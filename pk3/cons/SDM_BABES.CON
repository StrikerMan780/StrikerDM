action FEMSHRUNK
action FEMFROZEN1 1
action FEMGROW
action FEMFROZEN2
action FEMDANCE1 19 1 1 1 16
action FEMDANCE3 19 1 1 1 26
action FEMDANCE2 20 2 1 1 10
action FEMFLASH 18 1 1 1 32

action FEMANIMATESLOW 0 2 1 1 100
action TOUGHGALANIM   0 5 1 1 25
action FEMANIMATE

move FEMRESCUED
move FEMWAITRESPAWN

state babe_wait_respawn
	ifmultiplayer
	{
		ifvarand gametype_flags 1 { } // GAMETYPE_COOP
		else ifmove FEMWAITRESPAWN
		{
			ifcount BABERESPAWNTIME
			{
				setvar TARGET -1
				
				setactor[THISACTOR].picnum PICNUM_MEMORY
				setactor[THISACTOR].cstat CSTAT_MEMORY
				setactor[THISACTOR].extra TOUGH
				setactor[THISACTOR].xrepeat XREPEAT_MEMORY
				setactor[THISACTOR].yrepeat YREPEAT_MEMORY
				setactor[THISACTOR].pal PAL_MEMORY
				
				espawn TRANSPORTERSTAR
				gettiledata[PICNUM_MEMORY].ysize TEMP
				getactor[THISACTOR].z TEMP2
				mulvarvar TEMP YREPEAT_MEMORY
				shiftvarl TEMP 1
				subvarvar TEMP2 TEMP
				setactor[RETURN].z TEMP2
				setactor[RETURN].pal 1
				setactor[RETURN].shade -127
				setactor[RETURN].blend 255
				setvar RETURN 0
				
				resetcount
				move 0
				
				switch PICNUM_MEMORY
					case TOUGHGAL: action TOUGHGALANIM break
					case FEM10: action FEMANIMATESLOW break
					default: action FEMANIMATE break
				endswitch
			}
			
			return // Stop execution until we respawn
		}
	}
ends

state babe_hide_or_destroy
	move FEMWAITRESPAWN
	ifmultiplayer
	{
		ifvarand gametype_flags 1 // GAMETYPE_COOP
		{
			killit
		}
		else
		{
			cstat 32768
		}
	}
	else
	{
		killit
	}
ends

defstate toughgalspeech
	ifrnd 64
	{
		ifnosounds
			soundonce TOUGHGALSND1
	}
	else ifrnd 64
	{
		ifnosounds
			soundonce TOUGHGALSND2
	}
	else ifrnd 64
	{
		ifnosounds
			soundonce TOUGHGALSND3
	}
	else ifnosounds
		soundonce TOUGHGALSND4
ends

defstate toughgaltalk
	ifp pfacing
	ifpdistl 1280
	ifhitspace
		state toughgalspeech
ends

state femcode
	state babe_wait_respawn
	
	ifmove FEMRESCUED
		break
	
	ifactor NAKED1 { }
	else ifactor FEM6 { }
	else
	{
		fall
	}

	ifaction FEMSHRUNK
	{
		ifcount SHRUNKDONECOUNT
		{
			action FEMANIMATE
			cstat 257
		}
		else ifcount SHRUNKCOUNT
		{
			sizeto 40 40
		}
		else
		{
			state genericshrunkcode
		}
	}
	else ifaction FEMGROW
	{
		ifcount 32
		{
			respawnhitag
			guts JIBS4 20
			guts JIBS6 20
			spritepal 6
			
			ifactor NAKED1
			{
				debris SCRAP3 4
				ifinwater { }
				else
				{
					soundonce LADY_SCREAM
				}
			}
			else ifactor PODFEM1
			{
				debris SCRAP3 4
				soundonce LADY_SCREAM
			}
			else
			{
				soundonce LADY_SCREAM
			}
			
			sound SQUISHED
			killit
		}
		else
		{
			sizeto MAXXSTRETCH MAXYSTRETCH
		}
	}

	ifaction FEMFROZEN1
	{
		ifcount THAWTIME
		{
			action FEMANIMATE
			getlastpal
		}
		else ifcount FROZENDRIPTIME
		{
			ifactioncount 26
			{
				spawn WATERDRIP
				resetactioncount
			}
		}
		
		ifhitweapon
		{
			ifwasweapon FREEZEBLAST
			{
				strength 0
				break
			}
			lotsofglass 30
			sound GLASS_BREAKING
			respawnhitag
			ifrnd 84
				spawn BLOODPOOL
			killit
		}
		else ifp pfacing
		{
			ifpdistl FROZENQUICKKICKDIST
			{
				pkick
				break
			}
		}
	}
	else ifaction FEMFROZEN2
	{
		ifcount THAWTIME
		{
			ifactor TOUGHGAL
			{
				action TOUGHGALANIM
			}
			else ifactor FEM10
			{
				action FEMANIMATESLOW
			}
			else
			{
				action FEMANIMATE
			}

			getlastpal
		}
		else ifcount FROZENDRIPTIME
		{
			ifactioncount 26
			{
				spawn WATERDRIP
				resetactioncount
			}
		}

		ifhitweapon
		{
			ifwasweapon FREEZEBLAST
			{
				strength 0
				break
			}
			
			lotsofglass 30

			sound GLASS_BREAKING
			ifrnd 84
				spawn BLOODPOOL
			respawnhitag

			ifrnd 128
				sound DUKE_HIT_STRIPPER1
			else
				sound DUKE_HIT_STRIPPER2

			killit
		}
		else ifp pfacing
		{
			ifpdistl FROZENQUICKKICKDIST
			{
				pkick
				break
			}
		}
	}

	ifhitweapon
	{
		// Immortal Babes
		ifactor NAKED1
		{
			ifinwater { } // Drowned
			else ifvarg IMMORTAL_BABES 0
			{
				break
			}
		}
		else ifactor FEM8 { } // Dead by default
		else ifvarg IMMORTAL_BABES 0
		{
			break
		}
	
		ifdead
		{
			ifwasweapon GROWSPARK
			{
				cstat 0
				move 0
				sound ACTOR_GROWING
				action FEMGROW
				break
			}
			else ifwasweapon FREEZEBLAST
			{
				ifaction FEMSHRUNK
					break

				getactor[THISACTOR].picnum TEMP
				switch TEMP
					case NAKED1:
					case FEM5:
					case FEM6:
					case FEM8:
					case FEM9:
					case FEM10:
					case TOUGHGAL:
					case PODFEM1:
						action FEMFROZEN2
						break
					default:
						action FEMFROZEN1
						break
				endswitch

				move 0
				spritepal 1
				strength 0

				sound SOMETHINGFROZE
				break
			}

			ifrnd 128
				sound DUKE_HIT_STRIPPER1
			else
				sound DUKE_HIT_STRIPPER2

			respawnhitag
			state standard_jibs
			state random_wall_jibs
			spawn BLOODPOOL

			ifactor FEM1
			{
				money 5
			}
			else ifactor FEM2
			{
				money 7
				cstat 0
				cactor BARBROKE
			}
			else ifactor FEM3
			{
				money 4
			}
			else ifactor FEM7
			{
				money 8
			}

			ifactor FEM5
			{
				strength TOUGH
				cactor BLOODYPOLE
			}
			else ifactor FEM6
			{
				cstat 0
				cactor FEM6PAD
			}
			else ifactor FEM8
			{
				strength TOUGH
				cactor BLOODYPOLE
			}
			else
			{
				spritepal 6
				
				ifactor NAKED1
				{
					ifinwater { }
					else
					{
						soundonce LADY_SCREAM
					}
					
					getactor[THISACTOR].cstat TEMP
					ifvarand TEMP 1 { subvar TEMP 1 }
					ifvarand TEMP 256 { subvar TEMP 256 }
					setactor[THISACTOR].cstat TEMP
			
					debris SCRAP3 18
					spritepal 0
					cactor VINES
				}
				else ifactor PODFEM1
				{
					soundonce LADY_SCREAM
					debris SCRAP3 18
					killit
				}
				else
				{
					soundonce LADY_SCREAM
					killit
				}
			}
		}
		else
		{
			ifwasweapon SHRINKSPARK
			{
				sound ACTOR_SHRINKING
				move 0
				action FEMSHRUNK
				cstat 0
				break
			}
			else ifwasweapon GROWSPARK
			{
				sound EXPANDERHIT
			}
			
			ifactor FEM8
			{
				break
			}

			ifactor TOUGHGAL
				state toughgalspeech
			else
				sound SQUISHED

			guts JIBS6 1
		}
	}
ends

state killme
	ifinwater { }
	else
	{
		ifp pfacing
		ifhitspace
		{
			setvar TEMP 0
			ifactor NAKED1 // Extend the use range for the hanging babe sprites
			{
				ifinwater
					break
					
				getplayer[THISACTOR].i TEMP2
				ldist TEMP3 THISACTOR TEMP2
				ifvarl TEMP3 1280
				{
					ifpdistl 2048
						setvar TEMP 1
				}
			}
			else
			{
				ifpdistl 1280
					setvar TEMP 1
			}
		
			ifvarn TEMP 1
				break
			
			soundonce KILLME
		}
	}
ends

state tipsounds
	getplayer[THISACTOR].i TEMP
	
	// Cancel oof sounds
	stopactorsound TEMP DUKE_SEARCH
	stopactorsound TEMP DUKE_SEARCH2
	
	randvar RANDOM_SOUND 1
	switch RANDOM_SOUND
		case 0: actorsound TEMP DUKE_TIP1 break
		case 1: actorsound TEMP DUKE_TIP2 break
	endswitch
ends

state tipme
	ifp pfacing
	ifpdistl 1280
	ifhitspace
	{
		tip
		state tipsounds
			
		ifactor FEM1
			action FEMDANCE1
		else ifactor FEM3
			action FEMFLASH
	}
ends

state babe_give_items
	ifmultiplayer
	ifvarand gametype_flags 1 { } // GAMETYPE_COOP
	else
	{
		ifrnd 128
		{
			break // Smaller chance in DM.
		}
	}

	ifvare BABES_ALLSAVED 1
	{
		randvar TEMP 4
		switch TEMP
			case 0: espawn PLASMASPRITE break
			case 1: espawn FLAMETHROWERSPRITE break
			case 2: espawn RAILGUNSPRITE break
			case 3: espawn SSGSPRITE break
			case 4: espawn GROWAMMO break
		endswitch
	}
	else
	{
		randvar TEMP 48
		ifvare TEMP 0 espawn PLASMASPRITE else
		ifvare TEMP 1 espawn PLASMAAMMO else
		ifvare TEMP 2 espawn FLAMETHROWERSPRITE else
		ifvare TEMP 3 espawn FLAMETHROWERAMMO else
		ifvare TEMP 4 espawn RAILGUNSPRITE else
		ifvare TEMP 5 espawn RAILGUNAMMO else
		ifvare TEMP 6 espawn SSGSPRITE else
		ifvare TEMP 7 espawn SHOTGUNAMMO else
		ifvare TEMP 8 espawn SHOTGUNSPRITE else
		ifvare TEMP 9 espawn GROWSPRITEICON else
		ifvare TEMP 10 espawn GROWAMMO else
		ifvare TEMP 11 espawn SHRINKERSPRITE else
		ifvare TEMP 12 espawn CRYSTALAMMO else
		ifvare TEMP 13 espawn CHAINGUNSPRITE else
		ifvare TEMP 14 espawn BATTERYAMMO else
		ifvare TEMP 15 espawn FREEZESPRITE else
		ifvare TEMP 16 espawn FREEZEAMMO else
		ifvare TEMP 17 espawn DEVISTATORSPRITE else
		ifvare TEMP 18 espawn DEVISTATORAMMO else
		ifvare TEMP 19 espawn FIRSTGUNSPRITE else
		ifvare TEMP 20 espawn AMMO else
		ifvare TEMP 21 espawn RPGSPRITE else
		ifvare TEMP 22 espawn RPGAMMO else
		ifvare TEMP 23 espawn HBOMBAMMO else
		ifvare TEMP 24 espawn TRIPBOMBSPRITE else
		ifvare TEMP 25 espawn HEATSENSOR else
		ifvare TEMP 26 espawn AIRTANK else
		ifvare TEMP 27 espawn BOOTS else
		ifvare TEMP 28 espawn STEROIDS else
		ifvarl TEMP 37 espawn COLA else
		ifvarl TEMP 43 espawn SIXPAK else
		ifvarl TEMP 47 espawn ATOMICHEALTH else
		ifvarl TEMP 48 espawn DFGSPRITE else
		ifvarl TEMP 49 espawn DFGAMMO
	}
	
	setactorvar[RETURN].DESPAWN_TYPE 2
	setvar RETURN 0
ends

state babe_check_all_rescued
	ifvarvare BABES_RESCUED BABES_TOTAL
	ifvare BABES_ALLSAVED 0
	{
		redefinequote 500 ^08All babes saved!
		userquote 500
		globalsound ALL_BABES_SAVED
		setvar BABES_ALLSAVED 1
		
		headspritestat CURSPRITE STAT_PLAYER
		whilevarn CURSPRITE -1
		{
			getactor[CURSPRITE].extra TEMP
			ifvarg TEMP 0
			ifvarl TEMP MAXPLAYERHEALTH
			{
				setactor[CURSPRITE].extra MAXPLAYERHEALTH
			}
				
			nextspritestat CURSPRITE CURSPRITE
		}
	}
ends

state babe_add_rescued
	ifmultiplayer
	ifvarand gametype_flags 1 { } // GAMETYPE_COOP
	else
	{
		break
	}
	
	addvar BABES_RESCUED 1
	
	redefinequote 500 ^12Babe saved!^08 %d ^12/^08 %d
	qsprintf 500 500 BABES_RESCUED BABES_TOTAL
	userquote 500
ends

state babe_rescue_genericsound
	randvar RANDOM_SOUND 1
	switch RANDOM_SOUND
		case 0: sound BABE_SAVED01 break
		case 1: sound BABE_SAVED02 break
	endswitch
ends

state babe_rescue_sounds
	getactor[THISACTOR].picnum TEMP
	switch TEMP
		case FEM10:
			sound FEM10_SAVED
			break
		case NAKED1:
		case PODFEM1:
		case FEM5:
		case FEM6:
		case TOUGHGAL:
			// Nothing
			break
		default:
			state babe_rescue_genericsound
			break
	endswitch
ends

defstate duke_dead_babe_response
	ifp pfacing
	ifpdistl 1280
	ifhitspace
	{
		getplayer[THISACTOR].i TEMP
		randvar RANDOM_SOUND 2
		switch RANDOM_SOUND
			case 0: actorsound TEMP DUKE_BABEDEAD01 break
			case 1: actorsound TEMP DUKE_BABEDEAD02 break
			case 2: actorsound TEMP DUKE_HIT_STRIPPER2 break
		endswitch
	}
ends

defstate duke_rescued_babe_response
	ifvare TARGET -1
		break
	
	ifactor FEM10 break
	ifactor NAKED1 break
	ifactor PODFEM1 break
	ifactor FEM5 break
	ifactor FEM6 break
	ifactor TOUGHGAL break
	
	dist TEMP TARGET THISACTOR
	ifvarl TEMP 1280
	{
		randvar RANDOM_SOUND 8
		switch RANDOM_SOUND
			case 0: actorsound TARGET DUKE_BABEREPLY01 break
			case 1: actorsound TARGET DUKE_BABEREPLY02 break
			case 2: actorsound TARGET DUKE_BABEREPLY03 break
			case 3: actorsound TARGET DUKE_BABEREPLY04 break
			case 4: actorsound TARGET DUKE_BABEREPLY05 break
			case 5: actorsound TARGET DUKE_BABEREPLY06 break
			case 6: actorsound TARGET DUKE_BABEREPLY07 break
			case 7: actorsound TARGET DUKE_BABEREPLY08 break
			case 8: actorsound TARGET DUKE_BABEREPLY09 break
		endswitch
	}
ends

state rescueme
	ifactor NAKED1
	{
		ifinwater
		{
			state duke_dead_babe_response
			break // Drowned, can't be saved.
		}
	}
	else ifactor FEM8
	{
		state duke_dead_babe_response
		break // Impaled, can't be saved.
	}
	
	ifaction FEMSHRUNK break
	ifaction FEMGROW break
	ifaction FEMFROZEN1 break
	ifaction FEMFROZEN2 break
	
	ifmove FEMWAITRESPAWN
		break
	
	ifmove FEMRESCUED
	{
		ifcount 31 { }
		else ifcount 30
		{
			state babe_rescue_sounds
		}
		
		ifcount 69 { }
		else ifcount 68
		{
			ifrnd 32
				state duke_rescued_babe_response
		}
		
		ifcount 75
		{
			// Telepor Sparkles
			espawn BABETELEPORT
			gettiledata[PICNUM_MEMORY].xsize TEMP2
			mulvarvar TEMP2 XREPEAT_MEMORY
			shiftvarr TEMP2 6
			
			gettiledata[PICNUM_MEMORY].ysize TEMP3
			mulvarvar TEMP3 YREPEAT_MEMORY
			shiftvarr TEMP3 6
			
			ifvarvarg TEMP2 TEMP3
			{
				setactor[RETURN].xrepeat TEMP2
				setactor[RETURN].yrepeat TEMP2
			}
			else
			{
				setactor[RETURN].xrepeat TEMP3
				setactor[RETURN].yrepeat TEMP3
			}
			
			setactor[RETURN].blend 255
			setactor[RETURN].shade -127
			setactor[RETURN].cstat 2
			setvar RETURN 0
			
			ifactor PODFEM1 { }
			else
			{
				// Beam Out Effect
				espawn BABE_BEAM
				setactor[RETURN].shade -127
				setactor[RETURN].mdflags 16
				setactor[RETURN].pal 33 // White
				setactor[RETURN].xrepeat XREPEAT_MEMORY
				setactor[RETURN].yrepeat YREPEAT_MEMORY
				setactorvar[RETURN].PICNUM_MEMORY PICNUM_MEMORY
				
				ifactor FEM2
					setactorvar[RETURN].PICNUM_MEMORY FEM2NOPOLE
				else ifactor FEM5
					setactorvar[RETURN].PICNUM_MEMORY FEM5NOPOLE
				else ifactor FEM6
					setactorvar[RETURN].PICNUM_MEMORY FEM6NOPAD
				else ifactor NAKED1
					setactorvar[RETURN].PICNUM_MEMORY NAKED1NOVINE
				
				getactor[THISACTOR].cstat TEMP
				ifvarand TEMP 1
					xorvar TEMP 1
				ifvarand TEMP 256
					xorvar TEMP 256
				setactor[RETURN].cstat TEMP
				setvar RETURN 0
			}
			
			state babe_check_all_rescued
			
			respawnhitag
			resetcount
			move FEMWAITRESPAWN
			
			state babe_give_items
			
			ifactor FEM5
			{
				espawn CLEANPOLE
				setactor[RETURN].xrepeat XREPEAT_MEMORY
				setactor[RETURN].yrepeat YREPEAT_MEMORY
				setactor[RETURN].cstat CSTAT_MEMORY
				setactorvar[RETURN].DESPAWN_TYPE 2
				setvar RETURN 0
				state babe_hide_or_destroy
			}
			else ifactor FEM6
			{
				espawn EMPTYPAD
				setactor[RETURN].xrepeat XREPEAT_MEMORY
				setactor[RETURN].yrepeat YREPEAT_MEMORY
				setactor[RETURN].cstat CSTAT_MEMORY
				setactorvar[RETURN].DESPAWN_TYPE 2
				setvar RETURN 0
				state babe_hide_or_destroy
			}
			else ifactor PODFEM1
			{
				espawn EMPTYPOD
				setactor[RETURN].xrepeat XREPEAT_MEMORY
				setactor[RETURN].yrepeat YREPEAT_MEMORY
				setactor[RETURN].cstat CSTAT_MEMORY
				setactorvar[RETURN].DESPAWN_TYPE 2
				setvar RETURN 0
				state babe_hide_or_destroy
			}
			else ifactor NAKED1
			{
				espawn VINES
				setactor[RETURN].xrepeat XREPEAT_MEMORY
				setactor[RETURN].yrepeat YREPEAT_MEMORY
				
				getactor[THISACTOR].cstat TEMP
				ifvarand TEMP 1 { subvar TEMP 1 }
				ifvarand TEMP 256 { subvar TEMP 256 }
				setactor[RETURN].cstat TEMP
				
				setactorvar[RETURN].DESPAWN_TYPE 2
				setvar RETURN 0
				state babe_hide_or_destroy
			}
			else ifactor FEM2
			{
				espawn FEM2POLE
				setactor[RETURN].xrepeat XREPEAT_MEMORY
				setactor[RETURN].yrepeat YREPEAT_MEMORY
				setactor[RETURN].cstat CSTAT_MEMORY
				setactorvar[RETURN].DESPAWN_TYPE 2
				setvar RETURN 0
				state babe_hide_or_destroy
			}
			else
			{
				state babe_hide_or_destroy
			}
		}
		
		break // Rescued, so don't run the below test again
	}
	
	ifp pfacing
	ifhitspace
	{
		setvar TEMP 0
		ifactor NAKED1 // Extend the use range for the hanging babe sprites
		{
			getplayer[THISACTOR].i TEMP2
			ldist TEMP3 THISACTOR TEMP2
			ifvarl TEMP3 1280
			{
				ifpdistl 2048
					setvar TEMP 1
			}
		}
		else
		{
			ifpdistl 1280
				setvar TEMP 1
		}
		
		ifvarn TEMP 1
			break
			
		ifvare TARGET -1
			getplayer[THISACTOR].i TARGET
			
		resetcount
		state babe_add_rescued
		move FEMRESCUED
	}
ends

actor FEM1 TOUGH FEMANIMATE // Stripper
	ifaction FEMDANCE1
	{
		ifactioncount 2
			action FEMDANCE2
	}
	else ifaction FEMDANCE2
	{
		ifactioncount 8
			action FEMDANCE3
	}
	else ifaction FEMDANCE3
	{
		ifactioncount 2
			action FEMANIMATE
	}
	
	state femcode
	state rescueme
	state tipme
enda

actor FEM2 TOUGH FEMANIMATE // Poledancer
	state femcode
	state rescueme
	state tipme
enda

actor FEM3 TOUGH FEMANIMATE // Hooker
	ifaction FEMFLASH
	{
		ifactioncount 8
		action FEMANIMATE
	}
	
	state femcode
	state rescueme
	state tipme
enda

actor FEM4 TOUGH FEMANIMATE // Tribal Babe
	state femcode
	state rescueme
	state tipme
enda

actor FEM5 TOUGH FEMANIMATE // Pole Babe
	state femcode
	state rescueme
	state killme
enda

actor FEM6 TOUGH FEMANIMATE // Pad Babe
	state femcode
	state rescueme
	state killme
enda

actor FEM7 TOUGH FEMANIMATE // Asian Dancer
	state femcode
	state rescueme
	state tipme
enda

actor FEM8 TOUGH FEMANIMATE // Impaled Babe
	state femcode
	state rescueme
enda

actor FEM9 TOUGH FEMANIMATE
	state femcode
	state rescueme
enda

actor FEM10 TOUGH FEMANIMATESLOW
	state femcode
	state rescueme
	state tipme
enda

actor TOUGHGAL MANWOMANSTRENGTH TOUGHGALANIM
	state femcode
	state rescueme
	state toughgaltalk
enda

actor NAKED1 TOUGH FEMANIMATE // Hanging Babe
	state femcode
	state rescueme
	state killme
enda

actor PODFEM1 TOUGH FEMANIMATE // Pod Babe
	state femcode
	state rescueme
	state killme
enda

actor BLOODYPOLE TOUGH
	ifhitweapon
	{
		sound SQUISHED
		guts JIBS6 1	
		ifdead
		{
			spritepal 8
			state standard_jibs
			killit
		}
	}
enda

useractor notenemy WOMAN PIRATEGALSTRENGTH
	state femcode
	state rescueme
	state tipme
enda

useractor notenemy PIRATE1A PIRATEGALSTRENGTH
	state femcode 
	state rescueme
	state tipme
enda

useractor notenemy PIRATE3A PIRATEGALSTRENGTH
	state femcode 
	state rescueme
	state tipme
enda

state alienslime_damage	
	ifhitweapon
	{
		sound SQUISHED
		guts JIBS6 1	
		ifdead
		{
			spritepal 8
			state standard_jibs
			killit
		}
	}
ends

actor VINES 0
	state item_despawn_timer
enda

actor EMPTYPAD TOUGH
	state alienslime_damage
	state item_despawn_timer
enda

actor EMPTYPOD TOUGH
	state alienslime_damage
	state item_despawn_timer
enda

actor CLEANPOLE TOUGH
	state alienslime_damage
	state item_despawn_timer
enda

actor FEM2POLE REALLYTOUGH
	state item_despawn_timer
	ifhitweapon
	ifdead
	{
		getactor[THISACTOR].cstat TEMP
		ifvarand TEMP 1
		{
			xorvar TEMP 1
			setactor[THISACTOR].cstat TEMP
		}
		
		debris SCRAP2 3
		debris SCRAP6 3
		sound VENT_BUST
		cactor BARBROKE
	}
enda

actor BARBROKE 0
	state item_despawn_timer
enda
