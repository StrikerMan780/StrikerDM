state shoot_distance_sensitive_fireball
	eshoot FIREBALL
	getplayer[THISACTOR].i TEMP
	ldist TEMP2 THISACTOR TEMP
	divvar TEMP2 7
				
	getactor[RETURN].zvel TEMP3
	subvarvar TEMP3 TEMP2
	setactor[RETURN].zvel TEMP3 
	setvar RETURN 0
ends

spriteflags FLAMETHROWERFLAME 132 // SFLAG_NOSHADE + SFLAG_NOEVENTS
defineprojectile FLAMETHROWERFLAME PROJ_WORKSLIKE 49218
defineprojectile FLAMETHROWERFLAME PROJ_SPAWNS EXPLOSION2
defineprojectile FLAMETHROWERFLAME PROJ_EXTRA ENEMY_FLAME_STRENGTH
defineprojectile FLAMETHROWERFLAME PROJ_XREPEAT 16
defineprojectile FLAMETHROWERFLAME PROJ_YREPEAT 16
defineprojectile FLAMETHROWERFLAME PROJ_SXREPEAT 16
defineprojectile FLAMETHROWERFLAME PROJ_SYREPEAT 16
defineprojectile FLAMETHROWERFLAME PROJ_PAL 0
defineprojectile FLAMETHROWERFLAME PROJ_SOUND FLAMETHROWER_FIRE
defineprojectile FLAMETHROWERFLAME PROJ_ISOUND -1
defineprojectile FLAMETHROWERFLAME PROJ_VEL 644
defineprojectile FLAMETHROWERFLAME PROJ_RANGE 15
defineprojectile FLAMETHROWERFLAME PROJ_CSTAT 130
defineprojectile FLAMETHROWERFLAME PROJ_HITRADIUS 0
defineprojectile FLAMETHROWERFLAME PROJ_SHADE -127

spritenoshade FIREFLYSPAWNER
defineprojectile FIREFLYSPAWNER PROJ_WORKSLIKE 49218
defineprojectile FIREFLYSPAWNER PROJ_SPAWNS FIREFLY
defineprojectile FIREFLYSPAWNER PROJ_EXTRA 0
defineprojectile FIREFLYSPAWNER PROJ_XREPEAT 24
defineprojectile FIREFLYSPAWNER PROJ_YREPEAT 24
defineprojectile FIREFLYSPAWNER PROJ_PAL 0
defineprojectile FIREFLYSPAWNER PROJ_SOUND -1
defineprojectile FIREFLYSPAWNER PROJ_ISOUND -1
defineprojectile FIREFLYSPAWNER PROJ_VEL 644
defineprojectile FIREFLYSPAWNER PROJ_RANGE 6
defineprojectile FIREFLYSPAWNER PROJ_CSTAT 130
defineprojectile FIREFLYSPAWNER PROJ_HITRADIUS 0
defineprojectile FIREFLYSPAWNER PROJ_SHADE -127

// -----------------------------------------------------------------------------
// ACTIONS
// -----------------------------------------------------------------------------
action AFFDYING    50  1  1  1   50
action AFFBOSSSPAWN  0  1  5  1  1


// -----------------------------------------------------------------------------
// ACTIONS
// -----------------------------------------------------------------------------
move FFJETPACKHOVERVELS 64 -48

// -----------------------------------------------------------------------------
// AIS
// -----------------------------------------------------------------------------
ai AIFFSHRUNK  ATROOPJETPACK TROOPJETPACKVELS seekplayer
ai AIFFHOVER ATROOPJETPACK FFJETPACKHOVERVELS seekplayer
ai AIFFSHOOTFIREBALL ATROOPJETPACK FFJETPACKHOVERVELS  faceplayer
ai AIFFSHOOTFIREBALLGROUND   ATROOPSHOOT   TROOPSTOPPED  faceplayer

// -----------------------------------------------------------------------------
// STATES
// -----------------------------------------------------------------------------
state drop_flamethrowerammo
  ifrnd SPAWNAMMOODDS
    spawn FLAMETHROWERAMMO
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state randomdroporfly
    // debug 500
    ifcount 50
	{
        iffloordistl 48
		{
            ifrnd 128
			{
				ifai AIFFSHRUNK
				{
					spawn FIREFLYGROWEFFECT
				}
				
				resetcount
                ai AITROOPSEEKENEMY
            }
			else ifrnd 64
			{
				ifai AIFFSHRUNK
				{
					spawn FIREFLYGROWEFFECT
				}
				
				resetcount
				ai AIFFHOVER
			}
            else {
				resetcount
                ai AIFFSHRUNK
            }
        }
		else ifpdistg 32768
		{
			ifrnd 32
			{
				ifai AIFFSHRUNK
				{
					spawn FIREFLYGROWEFFECT
				}
				
				resetcount
                ai AITROOPSEEKENEMY
			}
		}
    }
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state fflyingstate
    // debug 400
    ifpdistl 3072
    {
		ifcount 64
		{
			ifai AIFFSHRUNK
			{
				spawn FIREFLYGROWEFFECT
			}
			
			resetcount
			sizeto FF_SIZEX FF_SIZEY
			ai AITROOPSEEKENEMY
			break
		}
    }
	
	ifai AIFFSHRUNK
	{
		getactor[THISACTOR].htg_t 0 TEMP
		modvar TEMP 30
		ifvare TEMP 0
		{
			spawn FIREFLYFLYINGEFFECT
		}
	}
	
    ifcount 120
	{
		ifrnd 51
		{
			ifai AIFFSHRUNK
			{
				spawn FIREFLYGROWEFFECT
			}
				
			ai AITROOPSEEKENEMY
		}
		else
		{
			ifai AIFFSHRUNK
			{
				spawn FIREFLYGROWEFFECT
			}
				
			ai AIFFSHOOTFIREBALL
		}
	}
	else ifcount 30
	{
		ifai AIFFHOVER
		{
			resetcount
			ai AIFFSHOOTFIREBALL
		}
	}

    state randomdroporfly
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state ffshrunkstate
    // debug 300
    ifcount 32 {
    }
    else ifcount 2 {
        sizeto FF_SHRUNKSIZEX FF_SHRUNKSIZEY
        spawn FRAMEEFFECT1
    }
	else {
		spawn FIREFLYSHRINKEFFECT
	}

    state fflyingstate
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state ffshootstate
    // debug 200
	ifcount 2
	{
		ifcanshoottarget
		{
			ifpdistl 6144
			{
				shoot FLAMETHROWERFLAME
				sound FLAMETHROWER_FIRE
				ifactioncount 10
				{
					resetactioncount
					//sound FLAMETHROWER_END
					ifrnd 128 {
						resetcount
						ai AIFFSHRUNK
					}
					else {
						ai AITROOPFLEEING
					}
				}
				else ifactioncount 1 { }
				else ifactioncount 0
				{
					soundonce FLAMETHROWER_START
				}
			}
			else
			{
				ai AITROOPSEEKPLAYER
			}
		}
		else
		{
			ai AITROOPSEEKPLAYER
		}
	}
ends

state ffshootfireballstate_ground
	ifcount 2
	{
		ifcanshoottarget
		{
			ifactioncount 2
			{
				ifp pducking
				{
					ifrnd 32
					{
						resetcount
						ai AITROOPDUCKING
						break
					}
				}
				
				state shoot_distance_sensitive_fireball
				sound FLAMETHROWER_START
				resetactioncount
					
				ifcount 48
				{
					ifpdistg 8192
						ai AITROOPSEEKPLAYER
					else
					{
						ifpdistg 7168
							ai AITROOPFLEEING
						else
							ai AITROOPFLEEINGBACK
					}
				}
			}
		}
		else
		{
			ai AITROOPSEEKPLAYER
		}
	}
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state ffshootfireballstate
    // debug 700
    sizeto FF_SIZEX FF_SIZEY
    ifpdistl 3072
    {
        ai AITROOPSEEKENEMY
    }
    else
	{
        ifcount 25
		{
			ifcanseetarget
			{
				sound FLAMETHROWER_START 
				
				eshoot FIREBALL
				getplayer[THISACTOR].i TEMP
				ldist TEMP2 THISACTOR TEMP
				divvar TEMP2 8
						
				getactor[RETURN].zvel TEMP3
				subvarvar TEMP3 TEMP2
				setactor[RETURN].zvel TEMP3 
				setvar RETURN 0
						
				resetcount
				ifrnd 32
				{
					resetcount
					ai AIFFSHRUNK
				}
				else ifrnd 32
				{
					iffloordistl 48
					{
						resetcount
						ai AITROOPSEEKENEMY
					}
					else
					{
						resetcount
						ai AIFFHOVER
					}
				}
			}
			else
			{
				resetcount
				ifrnd 150
				{
					ai AIFFHOVER
				}
				else
				{
					ai AITROOPSEEKENEMY
				}
			}
        }    
    }
ends


// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state ffseekstate
    ifcount 99 {
        ifpdistg 6144 {
			resetcount
            ai AIFFSHRUNK
        }
    }
  state troopgunnashoot
  ifinwater
  {
	resetcount
    ai AIFFSHRUNK
    break
  }
  ifcansee
  {
    ifp phigher
    {
      ifceilingdistl 128 { }
      else
        ifactornotstayput
		{
			ifcount 32
			{
				ifrnd 64
				{
					resetcount
					ai AIFFSHRUNK
				}
			}
		}
      break
    }
    else
      ifrnd 2
    {
      ifspritepal 21
        ifpdistg 1596
      {
        ai AITROOPHIDE
        break
      }
      ifbulletnear
      {
        ai AITROOPDODGE
        break
      }
    }
  }
  ifnotmoving
  {
    ifrnd 32
      operate
    else
      ifcount 32
        ifp palive
          ifcansee
            ifcanshoottarget
			{
				resetcount
				ai AITROOPSHOOTING
			}
  }
  else
  {
	ifrnd 8
	ifcount 32
	ifp palive
	ifcansee
	ifcanshoottarget
	ifpdistg 6144
	{
		resetcount
		ai AIFFSHOOTFIREBALLGROUND
	}
  }
  
  ifrnd 1
  {
    ifrnd 128
      soundonce PRED_ROAM
    else
      soundonce PRED_ROAM2
  }
ends


// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state ffexplode
	cstat 0
	spawn EXPLOSION2
	sound RPG_EXPLODE
	hitradius 2048 60 70 80 90
	strength 0
	move TROOPSTOPPED
	action ATROOPDEAD
	state standard_jibs
	state random_wall_jibs
	killit
ends

state ffdying
    // debug 600
	
	ifcount 2 { }
	else ifcount 1
	{
		spritepal 2
		soundonce FIRE_CRACKLE
	}
	
	state spawn_random_fire
	
	ifrnd 64 {
		state rf    
	}
	iffloordistl 32
    {
        ifcount 40
        {
			stopsound FIRE_CRACKLE
            state ffexplode
        }
        break
    }
    else
    {
        move 0
        action AFFDYING
    }
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state checkfireflyhit
    ifaction ATROOPSUFFERING
    {
        stopsound LIZARD_BEG
        sound PRED_DYING
        cstat 0
        strength 0
        action ATROOPSUFFERDEAD
        break
    }
    ifdead
    {
        ifwasweapon FREEZEBLAST
        {
            sound SOMETHINGFROZE
            spritepal 1
            move 0
            action ATROOPFROZEN
            strength 0
            break
        }

        state drop_flamethrowerammo
        state random_wall_jibs

        ifwasweapon GROWSPARK
        {
            cstat 0
            sound ACTOR_GROWING
            ai AITROOPGROW
            break
        }

        addkills 1

        ifwasweapon RPG
        {
            sound SQUISHED
            // state troop_body_jibs
            state standard_jibs
            killit
        }
        else ifwasweapon RADIUSEXPLOSION
        {
            sound SQUISHED
            // state troop_body_jibs
            state standard_jibs
            killit
        }
		else ifwasweapon RAILGUNSHOT
		{
			state ffexplode
		}
        else
        {
			ifai AIFFSHRUNK
			{
				spawn FIREFLYGROWEFFECT
			}
			
            sound PRED_DYING
            resetcount	
			ai AILIZDYING
			
			ifrnd 32
			iffloordistl 32
			{
				sound LIZARD_BEG
				spawn BLOODPOOL
				strength 0
				move 0
				action ATROOPSUFFERING
				break
			}
			
			ifwasweapon FIREBALL
			{
				action AFFDYING
			}
			else
			{
				ifrnd 80
				{
					action AFFDYING
				}
				else
				{
					action ATROOPDYING
				}
			}
			
            break
        }
    }
    else
    {
		ifwasweapon FLAMETHROWERFLAME {
			break
		}
		else {
			state random_wall_jibs
			sound PRED_PAIN
		}

        ifwasweapon SHRINKSPARK
        {
			sound ACTOR_SHRINKING
			resetcount
			ai AIFFSHRUNK
        }
        else ifwasweapon GROWSPARK
        {
            sound EXPANDERHIT
			ifai AIFFSHRUNK
			{
				spawn FIREFLYGROWEFFECT
				resetcount
				ifrnd 196
				{
					ai AIFFHOVER
				}
				else
				{
					ai AIFFSHOOTFIREBALL
				}
			}
        }
        else iffloordistl 32
        {
            ifrnd 96
            {
                action ATROOPFLINTCH
            }
        }
		else
		{
			ifrnd 32
			{
				ifai AIFFSHRUNK
				{
					spawn FIREFLYGROWEFFECT
					resetcount
					
					ifrnd 128
						ai AIFFSHOOTFIREBALL
					else
						ai AIFFHOVER
				}
			}
		}
    }
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state fireflycode
    fall
    ifinwater
    {
        ifrnd 1
        {
            spawn WATERBUBBLE
        }
    }
    ifaction ATROOPSTAND
    {
        ifrnd 51
		{
            // 20% chance of attacking
            resetcount
            ai AITROOPSHOOTING
        }
        else
		{
            // 80% chance of shrinking and flying
			resetcount
            ai AIFFSHRUNK
        }
    }
    else ifaction AFFBOSSSPAWN
    {
		resetcount
        ai AIFFSHRUNK
    }
    else ifaction ATROOPFROZEN
    {
        ifcount THAWTIME
        {
            ai AITROOPSEEKENEMY
            getlastpal
        }
        else ifcount FROZENDRIPTIME
        {
            ifactioncount 26
            {
                spawn WATERDRIP
                resetactioncount
            }
        }
        ifhitweapon
        {
            ifwasweapon FREEZEBLAST
            {
                strength 0
                break
            }
            addkills 1

            ifrnd 84
            spawn BLOODPOOL
            lotsofglass 30
            sound GLASS_BREAKING
            killit
        }
        ifp pfacing {
            ifpdistl FROZENQUICKKICKDIST {
                pkick
            }
        }
        break
    }
    else ifaction ATROOPPLAYDEAD
    {
        ifhitweapon
        {
            ifwasweapon RADIUSEXPLOSION
            {
                sound SQUISHED
                // state troop_body_jibs
                state standard_jibs
                killit
            }
            break
        }
        else
        {
            state checksquished
        }
        ifcount PLAYDEADTIME
        {
            addkills -1
            soundonce PRED_ROAM
            cstat 257
            strength 1
			resetcount
            ai AITROOPSHOOTING
        }
        else ifp pfacing {
            resetcount
        }

        break
    }
    else ifaction ATROOPDEAD
    {
        strength 0
        ifrespawn
        ifcount RESPAWNACTORTIME
        {
            spawn TRANSPORTERSTAR
            cstat 257
            strength FF_STRENGTH
            ai AITROOPSEEKENEMY
        }
        ifhitweapon
        {
            ifwasweapon RADIUSEXPLOSION
            {
                sound SQUISHED
                // state troop_body_jibs
                state standard_jibs
                killit
            }
            break
        }
        else
            state checksquished

        break
    }
    else ifaction ATROOPSUFFERDEAD
    {
        ifactioncount 2
        {
            ifrnd 64
            {
                resetcount
                action ATROOPPLAYDEAD
            }
            else
            {
                soundonce PRED_DYING
                action ATROOPDEAD
            }
        }
    }
    else ifaction AFFDYING
    {
		sizeto FF_SIZEX FF_SIZEY
        state ffdying
        break
    }
	else ifaction ATROOPDYING
    {
		sizeto FF_SIZEX FF_SIZEY
        state troopdying
        break
    }
    else ifaction ATROOPSUFFERING
    {
		sizeto FF_SIZEX FF_SIZEY
        state troopsufferingstate
        ifhitweapon {
            state checkfireflyhit
        }
        break
    }
    else ifaction ATROOPFLINTCH
    {
        ifactioncount 2
        ai AITROOPSEEKENEMY
    }
    else
    {
        ifai AITROOPSEEKPLAYER {
            // debug 1
			sizeto FF_SIZEX FF_SIZEY
            state ffseekstate
        }
        else ifai AITROOPSEEKENEMY {
            // debug 2
            sizeto FF_SIZEX FF_SIZEY // always try and embiggen the fireflytrooper if he's in a smaller state
            state ffseekstate
        }
        else ifai AITROOPSHOOTING {
            // debug 3
            sizeto FF_SIZEX FF_SIZEY
            state ffshootstate
        }
		else ifai AIFFSHOOTFIREBALLGROUND {
            // debug 3
            sizeto FF_SIZEX FF_SIZEY
            state ffshootfireballstate_ground
        }
        else ifai AITROOPFLEEING {
            // debug 4
            state troopfleestate
        }
        else ifai AITROOPFLEEINGBACK {
            // debug 5
            state troopfleestate
        }
        else ifai AIFFSHRUNK {
            // debug 6
            state ffshrunkstate
        }
        else ifai AIFFHOVER {
            // debug 7
			sizeto FF_SIZEX FF_SIZEY
            state fflyingstate
        }
        else ifai AITROOPGROW {
            // debug 8
            state genericgrowcode
        }
        else ifai AITROOPDUCKING {
            // debug 9
            sizeto FF_SIZEX FF_SIZEY
            state troopduckstate
        }
        else ifai AIFFSHOOTFIREBALL {
            // debug 10
            state ffshootfireballstate
        }
        else ifai AITROOPHIDE
        {
            // debug 11
            sizeto FF_SIZEX FF_SIZEY
            state troophidestate
            break
        }
    }

    ifhitweapon {
        state checkfireflyhit
    }
    else {
        state checksquished
    }
ends

spritenoshade FIREFLYSHRINKEFFECT
action SHRINK_FRAMES 0 7 1 1 4
actor FIREFLYSHRINKEFFECT 0 SHRINK_FRAMES
	ifactioncount 7 {
		killit
	}
	
	ifcount 1 { }
	else ifcount 0
	{
		sizeat 32 32
		cstator 2
		setactor[THISACTOR].blend 255
		setactor[THISACTOR].shade -127
	}
enda

spritenoshade FIREFLYGROWEFFECT
action GROW_FRAMES 0 7 1 -1 4
actor FIREFLYGROWEFFECT 0 GROW_FRAMES
	ifactioncount 7 {
		killit
	}	
	else ifcount 1 { }
	else ifcount 0
	{
		sizeat 32 32
		cstator 2
		setactor[THISACTOR].blend 255
		setactor[THISACTOR].shade -127
	}
enda

spritenoshade FIREFLYFLYINGEFFECT
action FLYING_FRAMES 0 7 1 1 4
actor FIREFLYFLYINGEFFECT 0 FLYING_FRAMES
	ifactioncount 7 {
		killit
	}
	
	ifcount 1 {	}
	else ifcount 0
	{
		sizeat 32 32
		cstator 2 
		setactor[THISACTOR].blend 255
		setactor[THISACTOR].shade -127
	}
enda

spriteflags FIREFLY SF_ENEMY_SLIMEFOOD_DAMAGEEVENT
actor FIREFLY FF_STRENGTH 0
    ifaction 0
	{
	    spritepal 12
		sound PRED_RECOG
        ifspawnedby FIREFLYSPAWNER
		{
			sizeat FF_SHRUNKSIZEX FF_SHRUNKSIZEY
            action AFFBOSSSPAWN
        }
        else
		{
            action ATROOPSTAND
        }
		spawn FIREFLYFLYINGEFFECT
    }
    state fireflycode
enda

// **************************************************

action ABOSS5WALK        0  4  5  1  30
action ABOSS5FROZEN        0  1  5
action ABOSS5RUN         0  4  5  1  15
action ABOSS5SHOOT         20 2  5  1  15
action ABOSS5BREATH         30 2  5  1  105
action ABOSS5DYING        40 8  1  1  35
action BOSS5FLINTCH       40 1  1  1  1
action ABOSS5DEAD         48

move PALBOSS5SHRUNKRUNVELS 32
move PALBOSS5RUNVELS 84
move BOSS5WALKVELS 192
move BOSS5RUNVELS 256
move BOSS5STOPPED
move BOSS5BARFED

ai AIBOSS5SEEKENEMY ABOSS5WALK BOSS5WALKVELS seekplayer
ai AIBOSS5RUNENEMY ABOSS5RUN BOSS5RUNVELS faceplayer
ai AIBOSS5SHOOTENEMY ABOSS5SHOOT BOSS5STOPPED faceplayer
ai AIBOSS5SHOOTENEMYFAR ABOSS5SHOOT BOSS5STOPPED faceplayer
ai AIBOSS5BREATHEFIRE ABOSS5BREATH BOSS5STOPPED faceplayer
ai AIBOSS5BARFENEMY ABOSS5BREATH BOSS5STOPPED faceplayer
ai AIBOSS5DYING ABOSS5DYING BOSS5STOPPED faceplayer
ai AIBOSS5PALSHRINK ABOSS5WALK PALBOSS5SHRUNKRUNVELS furthestdir

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5palshrunkstate
  ifcount SHRUNKDONECOUNT
  {
    cstat 257
    ai AITROOPSEEKENEMY
  }
  else
    ifcount SHRUNKCOUNT
      sizeto 40 40
  else
    state genericshrunkcode
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state checkBOSS5seekstate
  ai AIBOSS5SEEKENEMY
  ifspritepal 0 { }
  else   // a fake way of doing a ifspritepal NOT.
    move PALBOSS5RUNVELS seekplayer
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5runenemystate
	ifcansee
	{
		ifactioncount 3
		{
			ifcanshoottarget
			{
				resetactioncount
				sound BOS1_WALK
			}
			else
			{
				ai AIBOSS5SEEKENEMY
			}
		}
		
		ifcount 48
		{
			ifrnd 2
			{
				ifp palive
				{
					sound BOS2_ATTACK
					ai AIBOSS5SHOOTENEMY
				}
				break
			}
		}
		
		ifpdistl 3072
		{
			ifp palive
			{
				ai AIBOSS5BREATHEFIRE
			}
		}
	}
	else
	{
		ai AIBOSS5SEEKENEMY
	}
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5seekenemystate
	ifrnd 2
		soundonce BOS2_ROAM
	else ifactioncount 3
	{
		resetactioncount
		sound BOS1_WALK
	}

	ifcansee
	ifcount 32
	ifp palive
	ifrnd 48
	ifcanshoottarget
	{
		ifrnd 64
		ifpdistg 4096
		{
			ai AIBOSS5RUNENEMY
			ifspritepal 0
			{
				// Do Nothing
			}
			else
			{
				move PALBOSS5RUNVELS seekplayer
			}
			break
		}

		ifpdistl 3072
		{
			sound BOS2_ATTACK
			ai AIBOSS5BREATHEFIRE
		}
		else ifpdistl 10240
		{
			sound BOS2_ATTACK
			ai AIBOSS5SHOOTENEMY
		}
		else
		{
			ifrnd 128
			{
				sound BOS2_ATTACK
				ai AIBOSS5SHOOTENEMYFAR
			}
			else ifrnd 128
			{
				sound BOS2_ATTACK
				ai AIBOSS5BARFENEMY
			}
		}
	}
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5dyingstate
  ifaction ABOSS5DEAD
  {
    ifspritepal 0
      break
    ifrespawn
      ifcount RESPAWNACTORTIME
    {
      spawn TRANSPORTERSTAR
      cstat 257
      strength PIGCOPSTRENGTH
      state checkBOSS5seekstate
    }
    else
    {
      strength 0
      ifhitweapon
        ifwasweapon RADIUSEXPLOSION
      {
        sound SQUISHED
        state standard_jibs
        killit
      }
      break
    }
  }
  ifactioncount 8
  {
    iffloordistl 8
      sound THUD
    action ABOSS5DEAD
    cstat 0
    ifspritepal 0
        endofgame 52
  }
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5breathefire
	ifcount 72
	{
		stopsound FLAMETHROWER_FIRE
		state checkBOSS5seekstate
	}
	else ifaction ABOSS5BREATH
	{
		ifcanshoottarget
		ifactioncount 2
		{
			resetactioncount
		}
		else ifactioncount 1
		{
			shoot FLAMETHROWERFLAME
			shoot FLAMETHROWERFLAME
			shoot FLAMETHROWERFLAME
			shoot FLAMETHROWERFLAME
			sound FLAMETHROWER_FIRE
		}
	}
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------

state BOSS5barfenemy
	ifcansee
	{
		ifactioncount 2
		{
			move 0
			state checkBOSS5seekstate
		}
		else ifactioncount 1
		{
			ifmove BOSS5BARFED { }
			else
			{
				shoot FIREFLYSPAWNER
				move BOSS5BARFED
			}
		}
	}
	else
	{
		state checkBOSS5seekstate
	}
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5shootenemy
	ifcount 72 
    {
        state checkBOSS5seekstate
    }
    else 
    {
        ifaction ABOSS5SHOOT
		ifcanshoottarget
        {
            ifactioncount 2 
            {
				sound FLAMETHROWER_START
				state shoot_distance_sensitive_fireball
				state shoot_distance_sensitive_fireball
				state shoot_distance_sensitive_fireball
				state shoot_distance_sensitive_fireball
				state shoot_distance_sensitive_fireball
				state shoot_distance_sensitive_fireball
                resetactioncount
				
				ifpdistg 10240
				{
					ifrnd 32
					{
						ai AIBOSS5SHOOTENEMYFAR
					}
					else ifrnd 32
					{
						ai AIBOSS5RUNENEMY
					}
				}
            }
        }
		else
		{
			state checkBOSS5seekstate
		}
    }
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5shootenemyfar
    ifcount 52 
    {
        state checkBOSS5seekstate
    }
    else 
    {
        ifaction ABOSS5SHOOT 
		ifcanshoottarget
        {
            ifactioncount 2
            {
				state shoot_distance_sensitive_fireball
                shoot DEVASTATORMISSILE
				shoot DEVASTATORMISSILE
                resetactioncount
            }
			else ifactioncount 1
			{
				state shoot_distance_sensitive_fireball
				shoot DEVASTATORMISSILE
				shoot DEVASTATORMISSILE
			}
        }
		else
		{
			state checkBOSS5seekstate
		}
    }
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state checkBOSS5hitstate
  ifrnd 2
    spawn BLOODPOOL
  ifdead
  {
    ifspritepal 0
      globalsound DUKE_TALKTOBOSSFALL
    else
    {
      ifrnd 64
        globalsound DUKE_TALKTOBOSSFALL
      ifwasweapon FREEZEBLAST
      {
        sound SOMETHINGFROZE
        spritepal 1
        move 0
        action ABOSS5FROZEN
        strength 0
        break
      }
    }

    sound BOS2_DYING

    addkills 1

    ai AIBOSS5DYING
  }
  else
  {
	ifrnd 144
	{
		ifrnd 32
		{
			action BOSS5FLINTCH
			move 0
		}
		else
		{
			sound BOS2_ATTACK
			
			ifrnd 144
			{
				ai AIBOSS5SHOOTENEMY
			}
			else
			{
				ai AIBOSS5SHOOTENEMYFAR
			}
		}
    }

    ifspritepal 0 { }
    else
      ifwasweapon SHRINKSPARK
    {
      sound ACTOR_SHRINKING
      ai AIBOSS5PALSHRINK
      break
    }

    soundonce BOS2_PAIN

    debris SCRAP1 1
    guts JIBS6 1
  }
ends

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
state BOSS5code

  ifaction ABOSS5FROZEN
  {
    ifcount THAWTIME
    {
      ai AIBOSS5SEEKENEMY
        spritepal 21
    }
    else
      ifcount FROZENDRIPTIME
    {
      ifactioncount 26
      {
        spawn WATERDRIP
        resetactioncount
      }
    }

    ifhitweapon
    {
      ifwasweapon FREEZEBLAST
      {
        strength 0
        break
      }
      addkills 1

      lotsofglass 30
      sound GLASS_BREAKING
      ifrnd 84 spawn BLOODPOOL
      killit
    }
    ifp pfacing
      ifpdistl FROZENQUICKKICKDIST
        pkick
    break
  }
  ifai 0
  {
    ifspritepal 0
      ai AIBOSS5RUNENEMY
    else
    {
      ifspritepal 21
        strength BOSS5PALSTRENGTH
      sound BOS2_ATTACK ai AIBOSS5SHOOTENEMY
    }
  }
  else ifaction BOSS5FLINTCH
  {
    ifactioncount 3
      ai AIBOSS5SEEKENEMY
  }
  else ifai AIBOSS5SEEKENEMY
      state BOSS5seekenemystate
  else ifai AIBOSS5RUNENEMY
      state BOSS5runenemystate
  else ifai AIBOSS5SHOOTENEMY
      state BOSS5shootenemy
  else ifai AIBOSS5BREATHEFIRE
      state BOSS5breathefire
  else ifai AIBOSS5SHOOTENEMYFAR
      state BOSS5shootenemyfar
  else ifai AIBOSS5BARFENEMY
      state BOSS5barfenemy

  else
    ifai AIBOSS5PALSHRINK
      state BOSS5palshrunkstate

  ifai AIBOSS5DYING
    state BOSS5dyingstate
  else
  {
    ifhitweapon {
	  ifwasweapon FLAMETHROWERFLAME {
	  }
      else {
	  	state checkBOSS5hitstate
      }
	}
    else
      ifp palive
        ifspritepal 0 ifpdistl
          1280
    {
      addphealth -1000
      palfrom 63 63
    }
  }


ends

spriteflags BOSS5 SF_ENEMY_NOPUSH_DAMAGEEVENT
spriteflags BOSS5STAYPUT SF_ENEMYSTAYPUT_NOPUSH_DAMAGEEVENT
actor BOSS5 BOSS5STRENGTH fall state BOSS5code enda
actor BOSS5STAYPUT BOSS5STRENGTH cactor BOSS5 enda

definequote 666 ... ... hello? who goes there#?....
definequote 667 How did you find me?...
definequote 668 ...I've been so lonely, trapped here...
definequote 669 ..  my mind has been festering, rotting away...
definequote 670 . I'm so confused, I can barely remember a thing...
definequote 671 .. Do you know me? That's how you knew I was here, no?..#
definequote 672 ... I'm so hungry, so isolated, so alone...
definequote 673 ... so.... scared.... 

definequote 674 ... You can't hide forever.#.!.

definequote 675 .. # I see you! ...
definequote 676 ! Leave Me Alone!##...
definequote 677 .@ Get out of my head#@!...
definequote 678 .. Go away!!1#..

definequote 679 ####rrrrrrrraaaaaaaaaaaaaaaaaaaaaaaaaaaa###########!!!!!!....
definequote 680 ... I said to l@ave this place! ###..
definequote 681 .. NO! GET AWAY!111...
definequote 682 I ..... DO .... NOT... KNOW!.... YOU!....

definequote 687 ... Where did they go?!...
definequote 688 .. They're.... dead?.
definequote 689 You are filled with an incredible sense of dread...

action APPARITION_CHASE 0 4 1 1 2
action APPARITION_PATROL 0 4 1 1 4
action APPARITION_SPEAK 0 4 1 1 4
action APPARITION_IDLE 0 2 1 1 4
action APPARITION_WAIT 0 1 1 1 4
action APPARITION_RELOCATE 0 2 1 1 4
action APPARITION_TELEPORT_OUT 0 2 1 1 4
action APPARITION_TELEPORT_IN 0 2 1 1 4
action APPARITION_FINDTARGET 0 1 1 1 4

defstate apparition_playspeech
	randvar TEMP 5
	switch TEMP
		case 0: sound APPARITION_01 break
		case 1: sound APPARITION_02 break
		case 2: sound APPARITION_03 break
		case 3: sound APPARITION_04 break
		case 4: sound APPARITION_05 break
		case 5: sound APPARITION_06 break
	endswitch
ends

gamevar apparition_debug 0 0
defstate apparition_get_first_path_node
	ifvare NUM_PATHS 0 // No paths, bail.
		break
	
	setvar APPARITION_LOCATOR -1 // Clear our path.
	
	randvarvar APPARITION_PATHNUM NUM_PATHS
	modvarvar APPARITION_PATHNUM NUM_PATHS
	addvar APPARITION_PATHNUM 1
	
	// APPARITION_DEBUG
	ifvarg apparition_debug 0
		setvarvar APPARITION_PATHNUM apparition_debug
	
	headspritestat CURSPRITE STAT_LOCATOR
	whilevarn CURSPRITE -1
	{
		getactor[CURSPRITE].hitag TEMP
		getactor[CURSPRITE].lotag TEMP2
		ifvarvare APPARITION_PATHNUM TEMP
		ifvare TEMP2 0
		{
			setvarvar APPARITION_LOCATOR CURSPRITE
			getactor[APPARITION_LOCATOR].x DEST_X
			getactor[APPARITION_LOCATOR].y DEST_Y
			getactor[APPARITION_LOCATOR].z DEST_Z
			getactor[APPARITION_LOCATOR].sectnum TEMP
			changespritesect THISACTOR TEMP
			setsprite THISACTOR DEST_X DEST_Y DEST_Z
			setvar CURSPRITE -1
			
			ifvarn apparition_debug 0
			{
				redefinequote 500 FOUND LOCATOR %d ON PATH %d - IMPATIENCE: %d
				qsprintf 500 500 APPARITION_LOCATOR APPARITION_PATHNUM APPARITION_IMPATIENCE
				userquote 500
			}
			
			break
		}
		
		ifvarn CURSPRITE -1
			nextspritestat CURSPRITE CURSPRITE
	}
ends

defstate apparition_picktarget
	ifvarn APPARITION_STATE APPRSTATE_NEEDTARGET
		break
		
	// Pick a target
	setvar TEMP2 0
	setvar TEMP4 0 // Number of dead players
	ifvare MULTIMODE 1
	{
		setvar TEMP2 1
	}
	else whilevarn TEMP2 1
	{
		randvarvar TEMP MULTIMODE
		modvarvar TEMP MULTIMODE
		ifvarvarn TEMP APPARITION_TARGET
		{
			getplayer[TEMP].dead_flag TEMP3
			ifvarn TEMP3 0
				addvar TEMP4 1
			
			ifvarg TEMP4 2 // More than 2 dead players
				setvar TEMP2 1 // Set target anyway
			else ifvare TEMP3 0 // Not dead, let's target this guy.
				setvar TEMP2 1
		}
	}
		
	setvarvar APPARITION_TARGET TEMP
	setvar APPARITION_STATE APPRSTATE_NONE
	
	ifvarn apparition_debug 0
	{
		redefinequote 500 Changing Target to %d
		qsprintf 500 500 APPARITION_TARGET
		userquote 500
	}
ends

defstate apparition_teleport
	setvar STATE_PARAM1 16 // Intensity
	setvar STATE_PARAM2 8192 // Radius
	state start_shake
	
	ifvare APPARITION_STATE APPRSTATE_RELOCATE
		action APPARITION_RELOCATE
	else
		action APPARITION_TELEPORT_OUT
ends

defstate apparition_dotext
	setvar APPARITION_QTIME 120
	
	randvar TEMP 48
	subvar TEMP 24
	setvarvar APPARITION_QANGLE TEMP
	
	randvar APPARITION_QYOFF 100
	addvar APPARITION_QYOFF 60
	
	state apparition_playspeech
ends

defstate apparition_dodamage
	getactor[THISACTOR].htg_t 0 STATE_TEMP1
	modvar STATE_TEMP1 5
	ifvare STATE_TEMP1 0
	{
		hitradius 1024 9999 9999 9999 9999 // Instant fucking murder.
		
		// Anti-cheese, since hitradius eats shit on TROR.
		headspritestat CURSPRITE STAT_PLAYER
		whilevarn CURSPRITE -1
		{
			dist STATE_TEMP1 THISACTOR CURSPRITE
			ifvarl STATE_TEMP1 384
			{
				setvar STATE_PARAM1 9999
				setvarvar STATE_PARAM2 CURSPRITE
				setvarvar STATE_PARAM3 THISACTOR
				setvar STATE_PARAM4 RADIUSEXPLOSION
				setvar STATE_PARAM5 0
				state apply_damage
			}
			
			nextspritestat CURSPRITE CURSPRITE
		}
	}
ends

defstate apparition_beginpath
	resetcount
	action APPARITION_TELEPORT_IN
	
	state apparition_get_first_path_node
	state apparition_playspeech
	
	setvar STATE_PARAM1 16 // Intensity
	setvar STATE_PARAM2 8192 // Radius
	state start_shake
ends

//SFLAG_SHADOW(1) + SFLAG_NOSHADE(4) + SFLAG_NOPAL(64) + SFLAG_NOCLIP(2048) = 2117
spriteflags APPARITION 2117
actor APPARITION 0
	ifaction 0
	{
		ifvare APPARITION_SPRITEID -1
			setvarvar APPARITION_SPRITEID THISACTOR
		else
			killit // Can't have more than one of these guys.
		
		fall
		sizeat 44 40
		setvar APPARITION_STATE APPRSTATE_NONE
		action APPARITION_IDLE
	}
	
	ifaction APPARITION_IDLE
	{
		ifpdistl 1024
		ifp pfacing
		ifhitspace
		{
			state apparition_playspeech
			action APPARITION_SPEAK
			userquote 689
			resetcount
			
			getplayer[THISACTOR].i TEMP
			getactor[TEMP].yvel TEMP
			setvarvar APPARITION_TARGET TEMP
			setvar APPARITION_LAIRSPRITEID -2 // Clear the apparition's lair teleport.
			setvar APPARITION_STATE APPRSTATE_NEEDTARGET // Tell the apparition we need a new target.
			
			setvar APPARITION_QUOTE 666
			setvar APPARITION_QTIME 60
			
			randvar TEMP 48
			subvar TEMP 24
			setvarvar APPARITION_QANGLE TEMP
			
			setvar APPARITION_QYOFF 120
		}
	}
	else
	{
		sleeptime 0
	}
	
	ifaction APPARITION_SPEAK
	{
		sleeptime 0
		getactor[THISACTOR].htg_t 0 TEMP
		setvar TEMP2 0
		
		switch TEMP
			case 60:
				setvar APPARITION_QUOTE 667
				state apparition_dotext
				break
			case 120:
				setvar APPARITION_QUOTE 668
				state apparition_dotext
				break
			case 180:
				setvar APPARITION_QUOTE 669
				state apparition_dotext
				break
			case 240:
				setvar APPARITION_QUOTE 670
				state apparition_dotext
				break
			case 300:
				setvar APPARITION_QUOTE 671
				state apparition_dotext
				break
			case 420:
				setvar APPARITION_QUOTE 672
				state apparition_dotext
				break
			case 540:
				setvar APPARITION_QUOTE 673
				state apparition_dotext
				state apparition_teleport
				break
		endswitch
	}
	
	ifaction APPARITION_TELEPORT_OUT
	{
		sizeto 1 40
		sizeto 1 40
		getactor[THISACTOR].xrepeat TEMP
		ifvare TEMP 1
		{
			resetcount
			cstat 32768
			state apparition_picktarget
			action APPARITION_WAIT
			setvar APPARITION_STATE APPRSTATE_WAIT
			stopsound APPARITION_LOOP
		}
	}
	else ifaction APPARITION_RELOCATE
	{
		sizeto 1 40
		sizeto 1 40
		getactor[THISACTOR].xrepeat TEMP
		ifvare TEMP 1
		{
			cstat 32768
			stopsound APPARITION_LOOP
			
			state apparition_beginpath
		}
	}
	else ifaction APPARITION_TELEPORT_IN
	{
		getactor[THISACTOR].cstat TEMP
		ifvarand TEMP 32768
		{
			xorvar TEMP 32768
			setactor[THISACTOR].cstat TEMP
		}

		sizeto 40 40
		sizeto 40 40
		getactor[THISACTOR].xrepeat TEMP
		ifvarg TEMP 39
		{
			resetcount
			action APPARITION_PATROL
			setvar APPARITION_STATE APPRSTATE_PATROL
			soundonce APPARITION_LOOP
		}
	}
	else ifaction APPARITION_PATROL
	{
		ifvarn APPARITION_LOCATOR -1
		{
			dist TEMP THISACTOR APPARITION_LOCATOR
			ifvarl TEMP 128
			{
				getactor[APPARITION_LOCATOR].zvel TEMP
				ifvare TEMP 1 // Should we open a door?
					operate
					
				getactorvar[APPARITION_LOCATOR].EXTRA_MEMORY TEMP // Get next locator on path.
				ifvare TEMP -1 // End of the path, didn't find anyone. Teleport out.
				{
					ifvarn apparition_debug 0
					{
						redefinequote 500 END OF PATH: %d
						qsprintf 500 500 APPARITION_LOCATOR
						userquote 500
					}
					
					setvar APPARITION_LOCATOR -1 // Clear locator
					
					addvar APPARITION_IMPATIENCE 16 // Become more impatient.
					ifvarg APPARITION_IMPATIENCE 64
					{
						ifrnd 32
							setvar APPARITION_STATE APPRSTATE_NEEDTARGET
					}
					
					ifvarg APPARITION_IMPATIENCE 96
						setvar APPARITION_IMPATIENCE 96
					
					state apparition_teleport
					state apparition_playspeech
					break // Restart
				}
				else
				{
					setvarvar APPARITION_LOCATOR TEMP // Set our locator to this one.
					
					ifvarn apparition_debug 0
					{
						redefinequote 500 CHANGING TO LOCATOR: %d
						qsprintf 500 500 APPARITION_LOCATOR
						userquote 500
					}
					
					soundonce APPARITION_LOOP
					
					ifrnd 16
						state apparition_playspeech
				}
			}
			
			// Insert seek code here
			ifvarg APPARITION_TARGET -1
			{
				ifpdistl 6144
				ifcanseetarget
				ifrnd 4
				{
					getplayer[THISACTOR].heat_on TEMP // Nightvision pisses it off, making it change targets.
					
					ifvare TEMP 0
					{
						getplayer[THISACTOR].curr_weapon TEMP2
						ifvare TEMP2 DEVISTATOR_WEAPON // Devastator and DFG piss it off as well.
						{
							getplayer[THISACTOR].kickback_pic TEMP2
							ifvarg TEMP2 0
								setvar TEMP 1
						}
						
						ifvare TEMP 0
						ifp pfacing
						{
							getplayer[THISACTOR].kickback_pic TEMP2
							ifvarg TEMP2 0 // Shooting directly at him pisses him off
								setvar TEMP 1
						}
					}
					
					ifvare TEMP 1
					{
						getplayer[THISACTOR].i TEMP
						getactor[TEMP].yvel TEMP
						setvarvar APPARITION_TARGET TEMP
					}
				}
				
				getplayer[APPARITION_TARGET].i TEMP // Get player sprite index.
				getactor[TEMP].extra TEMP2
				ifvarg TEMP2 0 // Alive?
				{
					getactor[TEMP].xvel TEMP2 // Our speed influences how easily we're detected
					
					getinput[APPARITION_TARGET].bits TEMP3 // Check inputs
					ifvarand TEMP3 2 // 2 = SK_CROUCH. If we're crouching, we stand out less.
						addvar TEMP2 8
					else
						addvar TEMP2 32
						
					getplayer[APPARITION_TARGET].kickback_pic TEMP3
					ifvarg TEMP3 0
						addvar TEMP2 255 // Shooting makes you a prime target!
						
					getplayer[APPARITION_TARGET].jumping_counter TEMP3 // Don't jump you fucker, he'll see you.
					ifvarg TEMP3 180
						addvar TEMP2 64
						
					getplayer[APPARITION_TARGET].heat_on TEMP3
					ifvare TEMP3 1
						addvar TEMP2 255 // He'll hate you for using nightvision.
					
					// Get distance.
					dist TEMP3 THISACTOR TEMP
					ifvarl TEMP3 2048
						addvar TEMP2 128 // You're too close, he'll more likely be able to see you.
					else ifvarl TEMP3 4096
						addvar TEMP2 64
					else ifvarl TEMP3 6144
						addvar TEMP2 16
					else ifvarg TEMP3 8192 // You're far away, he'll be less likely to see you.
						subvar TEMP2 8 
					else ifvarg TEMP3 10240 
						subvar TEMP2 16
						
					ifvarl TEMP2 0
						setvar TEMP2 0
						
					randvar TEMP3 1536
					ifvarvarg TEMP2 TEMP3
					{
						canseespr THISACTOR TEMP TEMP4
						ifvare TEMP4 0
						{
							getactor[THISACTOR].z TEMP3
							subvar TEMP3 6144
							setactor[THISACTOR].z TEMP3
							
							getactor[TEMP].z TEMP3
							subvar TEMP3 6144
							setactor[TEMP].z TEMP3
							
							canseespr THISACTOR TEMP TEMP4
							
							getactor[THISACTOR].z TEMP3
							addvar TEMP3 6144
							setactor[THISACTOR].z TEMP3
							
							getactor[TEMP].z TEMP3
							addvar TEMP3 6144
							setactor[TEMP].z TEMP3
						}
						
						ifvare TEMP4 1
						{
							setplayer[APPARITION_TARGET].fta 100
							setplayer[APPARITION_TARGET].ftq 197
							action APPARITION_CHASE
							setvar APPARITION_STATE APPRSTATE_CHASE
							setvar APPARITION_IMPATIENCE 0
							
							randvar TEMP 3
							switch TEMP
								case 0:
									setvar APPARITION_QUOTE 675
									break
								case 1:
									setvar APPARITION_QUOTE 676
									break
								case 2:
									setvar APPARITION_QUOTE 677
									break
								case 3:
									setvar APPARITION_QUOTE 678
									break
							endswitch
							
							state apparition_dotext
							state apparition_playspeech
							
							setplayervar[APPARITION_TARGET].SHAKE_INTENSITY 64
							setplayervar[APPARITION_TARGET].SHAKE_TIME 20
							
							randvar TEMP 4
							addvar TEMP 12
							setplayervar[APPARITION_TARGET].SCAREPIC IMSUFFERING
							setplayervar[APPARITION_TARGET].SCARETIME TEMP
							setplayervar[APPARITION_TARGET].SCAREPAL 0
							
							getplayer[APPARITION_TARGET].i STATE_PARAM1
							state random_stinger
						}
					}
				}
			}
			
			state apparition_dodamage
			
			// Set speed. The more impatient the apparition is, the faster it goes.
			setvar TEMP 64
			addvarvar TEMP APPARITION_IMPATIENCE
			setactor[THISACTOR].xvel TEMP
			
			// Move
			setvarvar STATE_PARAM1 APPARITION_LOCATOR
			setvarvar STATE_PARAM2 128
			setvarvar STATE_PARAM3 0
			state home_on_target
		}
	}
	else ifaction APPARITION_CHASE
	{
		state apparition_dodamage
		
		ifvare APPARITION_STATE APPRSTATE_NEEDTARGET
		{
			state apparition_teleport
			break
		}
		
		ifvare APPARITION_STATE APPRSTATE_RELOCATE
		{
			randvar TEMP 1
			switch TEMP
				case 0:
					setvar APPARITION_QUOTE 687
					break
				case 1:
					setvar APPARITION_QUOTE 688
					break
			endswitch
			
			state apparition_dotext
			state apparition_teleport
			break
		}
		
		getplayer[APPARITION_TARGET].i TEMP // Get player sprite.
		setvarvar STATE_PARAM1 TEMP // Target
		setvarvar STATE_PARAM2 128
		setvarvar STATE_PARAM3 0
		setactor[THISACTOR].xvel 150
		state home_on_target
		
		ifrnd 16
		{
			randvar TEMP 3
			switch TEMP
				case 0:
					setvar APPARITION_QUOTE 679
					break
				case 1:
					setvar APPARITION_QUOTE 680
					break
				case 2:
					setvar APPARITION_QUOTE 681
					break
				case 3:
					setvar APPARITION_QUOTE 682
					break
			endswitch
			
			state apparition_dotext
			state apparition_playspeech
			
			setplayervar[APPARITION_TARGET].SHAKE_INTENSITY 64
			setplayervar[APPARITION_TARGET].SHAKE_TIME 20
		}
	}
	
	// Done here, because we don't want more than one of these guys anyway.
	ifvarg APPARITION_QTIME 0
		subvar APPARITION_QTIME 1
		
	ifaction APPARITION_WAIT
	{
		ifvarg APPARITION_IMPATIENCE 95
		{
			ifcount 7
				state apparition_beginpath
		}
		else ifvarg APPARITION_IMPATIENCE 80
		{
			ifcount 15
				state apparition_beginpath
		}
		else ifvarg APPARITION_IMPATIENCE 63
		{
			ifcount 30
				state apparition_beginpath
		}
		else ifvarg APPARITION_IMPATIENCE 31
		{
			ifcount 75
				state apparition_beginpath
		}
		else ifvarg APPARITION_IMPATIENCE 15
		{
			ifcount 150
				state apparition_beginpath
		}
		else
		{
			ifcount 300
				state apparition_beginpath
		}
		break
	}
	
	cstat 0
	ifrnd 8
		cstator 4
		
	ifrnd 32
		cstator 2
	else ifrnd 8
		cstator 514
enda
