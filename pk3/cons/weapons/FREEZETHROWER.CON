state draw_freezer // FREEZE_WEAPON:
    state reset_hud_weapon_coordinates
    
    setvar hud_tilenum FREEZE
    
    setvar hud_temp3 0
    
    ifvarg weaponcount 0
    {
        addvar hud_totaltime 1
        ifvarvarl weaponcount hud_totaltime
        {
            state determine_animation
            ifvarn hud_temp 1
            {
                displayrand hud_temp
                andvar hud_temp 3
                
                addvarvar hud_x hud_temp
				
				displayrand hud_temp
                andvar hud_temp 3
				
                addvarvar hud_y hud_temp
            }
            
			// Base
            guniqhudid W_FREEZE_BASE
            
            addvar hud_x 210
            addvar hud_y 277 // 16 + 261
            
            addvar hud_tilenum 2
            setvar hud_shade -32
            
			orvar hud_orientation DRAWEAP_NOLERP
            state G_DrawWeaponTile
			// ----
            
			// Muzzle
            guniqhudid W_FREEZE_TOP
            
            subvar hud_y 26 // relative
            
            setvarvar hud_temp weaponcount
            modvar hud_temp 6
            divvar hud_temp 2
            addvar hud_tilenum 1
            addvarvar hud_tilenum hud_temp
            
			orvar hud_orientation DRAWEAP_NOLERP
            state G_DrawWeaponTile
			// ----
			
			guniqhudid 0
            
            setvar hud_temp3 1
        }
    }
    ifvare hud_temp3 0
    {
        guniqhudid W_FREEZE_BASE
        
        addvar hud_x 210
        addvar hud_y 261
        
        state G_DrawWeaponTile
        
        guniqhudid 0
    }
ends

actor FREEZESPRITE
    state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
	}
    else ifp pshrunk
	{
	}
    else ifp palive ifpdistl RETRIEVEDISTANCE ifcount 6 ifcanseetarget
    {
		// Parameters
		setvar STATE_PARAM1 FREEZE_WEAPON
		setvar STATE_PARAM2 WEAP_PRIMARY
		setvar STATE_PARAM3 1
		
		state check_have_weapon
		ifvare STATE_RETURN 1
			state checkweaponstay
		
		getplayervar[THISACTOR].FREEZETHROWER_AMMO TEMP
		ifvarl TEMP MAXFREEZEAMMO // Ammo under max?
		{
			addvarvar TEMP FREEZEAMMOAMOUNT
			ifvarg TEMP MAXFREEZEAMMO // If over, cap it.
			{
				setvar TEMP MAXFREEZEAMMO
			}
			
			setplayervar[THISACTOR].FREEZETHROWER_AMMO TEMP
		}
		else
		{
			ifvare STATE_RETURN 1 // Has freezethrower?
			{
				break // Has weapon, and full ammo. Stop here.
			}
		}
		
		state set_have_weapon
		state addweapon_nobreak
        quote 59
        ifspawnedby FREEZESPRITE
		{
            state getweaponcode
		}
        else
		{
            state quikweaponget_custom
		}
    }
enda