defineprojectile RAILGUNSHOT PROJ_TNUM 512
defineprojectile RAILGUNSHOT PROJ_TRAIL RAILGUNTRAIL
defineprojectile RAILGUNSHOT PROJ_TXREPEAT 12
defineprojectile RAILGUNSHOT PROJ_TYREPEAT 12
defineprojectile RAILGUNSHOT PROJ_OFFSET 224
defineprojectile RAILGUNSHOT PROJ_EXTRA RAILGUN_WEAPON_STRENGTH
defineprojectile RAILGUNSHOT PROJ_EXTRA_RAND 0
defineprojectile RAILGUNSHOT PROJ_SPAWNS LDEXPLOSION
defineprojectile RAILGUNSHOT PROJ_SXREPEAT 16
defineprojectile RAILGUNSHOT PROJ_SYREPEAT 16
defineprojectile RAILGUNSHOT PROJ_WORKSLIKE 8388609

state draw_railgun // RAILGUN_WEAPON:
	guniqhudid W_RAILGUN
	setvarvar hud_x weapon_xoffset
	setvarvar hud_y looking_arc
	shiftvarl hud_y 1
	subvarvar hud_y gun_pos
		
	ifvarg weaponcount 1
	ifvarl weaponcount 18
	{
		setvarvar hud_temp weaponcount
		shiftvarl hud_temp 7
		addvar hud_temp 768
		andvar hud_temp 2047
		sin hud_temp hud_temp
		shiftvarr hud_temp 11
		
		ifvarl weaponcount 10
		{
			shiftvarl hud_temp 2
		}
		
		subvarvar hud_x hud_temp
		subvarvar hud_y hud_temp
		subvarvar hud_angle hud_temp
		
		shiftvarl hud_temp 9
		subvarvar hud_scale hud_temp
	}
	
	addvar hud_x 246
	addvar hud_y 232
	
	switch weaponcount
		case 1
			setvar hud_tilenum RAILGUN2
			break
		case 2
			setvar hud_tilenum RAILGUN3
			break
		case 3
			setvar hud_tilenum RAILGUN4
			break
		case 4
			setvar hud_tilenum RAILGUN5
			break
		case 5
			setvar hud_tilenum RAILGUN6
			break
		case 6
			setvar hud_tilenum RAILGUN7
			break
		default
			setvar hud_tilenum RAILGUN1
			break
	endswitch

	ifvarg weaponcount 0
		orvar hud_orientation RS_FORCELERP
		
	state G_DrawWeaponTile
	guniqhudid 0
ends

actor RAILGUNSPRITE
    state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
	}
    else ifp pshrunk
	{
	}
    else ifp palive ifpdistl RETRIEVEDISTANCE ifcount 6 ifcanseetarget
    {
		// Parameters
		setvar STATE_PARAM1 RPG_WEAPON
		setvar STATE_PARAM2 WEAP_SECONDARY
		setvar STATE_PARAM3 1
		
		state check_have_weapon
		ifvare STATE_RETURN 1
			state checkweaponstay
		
		getplayervar[THISACTOR].RAILGUN_AMMO TEMP
		ifvarl TEMP MAXRAILGUNAMMO // Ammo under max?
		{
			addvarvar TEMP RAILGUNAMMOAMOUNT
			ifvarg TEMP MAXRAILGUNAMMO // If over, cap it.
			{
				setvar TEMP MAXRAILGUNAMMO
			}
			
			setplayervar[THISACTOR].RAILGUN_AMMO TEMP
		}
		else
		{
			ifvare STATE_RETURN 1 // Has weapon?
			{
				break // Has weapon, and full ammo. Stop here.
			}
		}
		
		state set_have_weapon
		state addweapon_nobreak
        quote 131
        ifspawnedby RAILGUNSPRITE
		{
            state getweaponcode
		}
        else
		{
            state quikweaponget_custom
		}
    }
enda

actor RAILGUNAMMO
    state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
    }
    else ifp pshrunk
	{
    }
    else ifp palive ifcount 6 ifpdistl RETRIEVEDISTANCE ifcanseetarget 
    {
		getplayervar[THISACTOR].RAILGUN_AMMO TEMP
		ifvarl TEMP MAXRAILGUNAMMO
		{
			addvarvar TEMP RAILGUNAMMOAMOUNT
			ifvarg TEMP MAXRAILGUNAMMO
			{
				setvar TEMP MAXRAILGUNAMMO
			}
			
			setplayervar[THISACTOR].RAILGUN_AMMO TEMP
			
			quote 132
			ifspawnedby RAILGUNAMMO {
				state getcode_custom
			}
			else {
				state quikget
			}
		}
    }
enda