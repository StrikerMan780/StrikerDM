state draw_shotgun // SHOTGUN_WEAPON:
	state reset_hud_weapon_coordinates
	
	subvar hud_x 8
	
	guniqhudid W_SHOTGUN
	
	switch weaponcount
		case 0
		case 1
		case 2
			addvar hud_x 146
			addvar hud_y 202
			
			ifvare weaponcount 2
			{
				addvar hud_x 4
				addvar hud_y 2
				addvar hud_angle 8
			}
			
			setvar hud_tilenum SHOTGUN 
			state G_DrawWeaponTile
			
			// Flash
			guniqhudid W_SHOTGUN_MUZZLE
			orvar hud_orientation RS_FORCELERP
			
			ifvare weaponcount 0 // Hack to prevent flash from going out of alignment due to bob interp.
				setvar hud_alpha 255
			else
				setvar hud_shade -128
			
			ifvare weaponcount 1
				setvar hud_tilenum SHOTGUN_FLASHEXTRA
			else
				setvar hud_tilenum SHOTGUN_FLASH1
			
			state G_DrawWeaponTile 
			setvarvar hud_shade gs
			break
		case 6
		case 7
		case 8
			addvar hud_x 146
			addvar hud_y 202
			
			setvar hud_tilenum SHOTGUN

			ifvare weaponcount 6
			{
				addvar hud_x -4
				addvar hud_y 5
				addvar hud_angle -16
			}
			else ifvare weaponcount 8
			{
				addvar hud_x 4
				addvar hud_y -5
				addvar hud_angle 16
			}
			
			state G_DrawWeaponTile
			break
		case 3
		case 4
		case 5
		case 9
		case 10
		case 11
		case 12
			addvar hud_x 158
			addvar hud_y 220
			
			setvar hud_tilenum SHOTGUN
			addvar hud_tilenum 3
			
			ifvare weaponcount 3
			{
				addvar hud_y 20
				addvar hud_x 10
			}
			else ifvare weaponcount 4
			{
				addvar hud_y 40
				addvar hud_x 20
			}
			else ifvare weaponcount 10
			{
				//addvar hud_angle 8
			}
			else ifvare weaponcount 11
			{
				addvar hud_x 6
				addvar hud_y -8
				addvar hud_angle 32
			}
			else ifvare weaponcount 12
			{
				addvar hud_x 12
				addvar hud_y -16
				addvar hud_angle 64
			}
			
			state G_DrawWeaponTile
			
			// Flash
			ifvarl weaponcount 5
			{
				guniqhudid W_SHOTGUN_MUZZLE
				setvar hud_shade -128
				setvar hud_tilenum SHOTGUN_FLASH2
				
				//orvar hud_orientation RS_FORCELERP
				state G_DrawWeaponTile
				setvarvar hud_shade gs
			}
			break
		case 13
		case 14
		case 15
		case 28
		case 29
			addvar hud_x 198 // 32 + 166
			addvar hud_y 210
			
			setvar hud_tilenum SHOTGUN
			addvar hud_tilenum 4
			
			ifvare weaponcount 13
			{
				addvar hud_x -8
				addvar hud_y 10
				addvar hud_angle -32
			}
			else ifvare weaponcount 15
			{
				addvar hud_x 8
				addvar hud_y -8
				//addvar hud_angle 32
			}
			else ifvare weaponcount 29
			{
				addvar hud_x -8
				addvar hud_y 10
				addvar hud_angle -32
			}
			
			state G_DrawWeaponTile
			break
		case 18
		case 19
		case 24
		case 25
			// Extra frame 1 (Mid-pump)
			addvar hud_x 234 // 64 + 170
			addvar hud_y 196
			
			setvar hud_tilenum SHOTGUN
			
			ifvarl weaponcount 24 // With shell
			{
				addvar hud_tilenum 8
			}
			else // No shell
			{
				addvar hud_tilenum 7
			}
			
			ifvare weaponcount 19
			{
				addvar hud_x 1
				addvar hud_y 2
			}
			else ifvare weaponcount 24
			{
				addvar hud_x 1
				addvar hud_y 2
			}
			
			state G_DrawWeaponTile
			break
		case 16
		case 17
		case 26
		case 27
			addvar hud_x 234 // 64 + 170
			addvar hud_y 196
			
			setvar hud_tilenum SHOTGUN
			addvar hud_tilenum 5
			
			ifvare weaponcount 16
			{
				addvar hud_x -8
				addvar hud_y 16
				addvar hud_angle -64
			}
			
			ifvare weaponcount 27
			{
				addvar hud_x -8
				addvar hud_y 16
				addvar hud_angle -64
			}
			
			state G_DrawWeaponTile
			break
		case 20
		case 21
		case 22
		case 23
			addvar hud_x 240 // 64 + 176
			addvar hud_y 196
			
			setvar hud_tilenum SHOTGUN
			
			ifvarl weaponcount 22 // With shell
			{
				addvar hud_tilenum 9
			}
			else // No shell
			{
				addvar hud_tilenum 6
			}
			
			ifvare weaponcount 21
			{
				addvar hud_x 1
				addvar hud_y 2
			}
			else ifvare weaponcount 22
			{
				addvar hud_x 2
				addvar hud_y 4
			}
			else ifvare weaponcount 23
			{
				addvar hud_x 1
				addvar hud_y 2
			}
			
			state G_DrawWeaponTile
			break
	endswitch
	
	guniqhudid 0
	
	// Shell HUD Code
	ifvare weaponcount 21
	{
		setvar hud_timer 0
	}
	
	ifvarg weaponcount 21
	ifvarl weaponcount 24
	{
		getuserdef[].pause_on hud_temp
		ifvare hud_temp 0
		ifvarg clock_diff 0
		{
			addvarvar hud_timer clock_diff
		}
		
		ifvarl hud_timer 9
		{
			//redefinequote 400 hud_timer: %d
			//qsprintf 400 400 hud_timer
			//userquote 400
			
			state reset_hud_weapon_coordinates
			
			setvar hud_tilenum 2535
			setvar hud_x 267
			setvar hud_y 188
			setvar hud_angle 2048
			
			setvar hud_temp2 16
			mulvarvar hud_temp2 hud_timer
			subvarvar hud_x hud_temp2
			
			setvar hud_temp2 1
			mulvarvar hud_temp2 hud_timer
			subvarvar hud_y hud_temp2
			
			setvar hud_temp2 128
			mulvarvar hud_temp2 hud_timer
			subvarvar hud_angle hud_temp2
			
			state G_DrawWeaponTile
		}
	}
ends

actor SHOTGUNSPRITE
  state item_shared_code
  
  ifmove RESPAWN_ACTOR_FLAG
	state respawnit
  else
	ifp pshrunk nullop
	else
	  ifp palive
		ifpdistl RETRIEVEDISTANCE
		  ifcount 6
			ifcanseetarget
	  {
		// Parameters
		setvar STATE_PARAM1 SHOTGUN_WEAPON
		setvar STATE_PARAM2 WEAP_PRIMARY
		setvar STATE_PARAM3 1
		
		state check_have_weapon
		ifvare STATE_RETURN 1
			state checkweaponstay
			
		getplayer[THISACTOR].ammo_amount SHOTGUN_WEAPON TEMP
			
		ifvarl TEMP MAXSHOTGUNAMMO // Ammo under max?
		{
			// Store Param 2
			setvarvar TEMP STATE_PARAM2
			
			ifspawnedby PIGCOP
			{
				randvar STATE_PARAM2 3
				addvar STATE_PARAM2 1
			}
			else { setvar STATE_PARAM2 SHOTGUNAMMOAMOUNT }
			state addammo_nobreak
			
			// Restore Param 2
			setvarvar STATE_PARAM2 TEMP
		}
		else
		{
			ifvare STATE_RETURN 1 // Has weapon?
			{
				break // Has weapon, and full ammo. Stop here.
			}
		}
		
		state set_have_weapon
		state addweapon_nobreak
		quote 57

		ifspawnedby SHOTGUNSPRITE
		  state getweaponcode
		else
		  state quikweaponget_custom
	  }
enda

actor SHOTGUNAMMO
  state item_shared_code
  
  ifmove RESPAWN_ACTOR_FLAG
	state respawnit
  else
	ifp pshrunk nullop
	else
	  ifp palive
		ifcount 6
		  ifpdistl RETRIEVEDISTANCE
			ifcanseetarget
	  {
		addammo SHOTGUN_WEAPON SHOTGUNAMMOAMOUNT
		quote 69
		ifspawnedby SHOTGUNAMMO
		  state getcode
		else
		  state quikget
	  }
enda