state draw_rpg // RPG_WEAPON:
    
    setvarvar hud_x weapon_xoffset
    setvarvar hud_y looking_arc
    shiftvarl hud_y 1
    subvarvar hud_y gun_pos
    
    setvarvar hud_temp weaponcount
    shiftvarl hud_temp 7
    addvar hud_temp 768
    andvar hud_temp 2047
    sin hud_temp hud_temp
    shiftvarr hud_temp 11
    
    subvarvar hud_x hud_temp
    subvarvar hud_y hud_temp
    
    addvar hud_x 164
    
    addvar hud_y 176
    
    setvar hud_tilenum RPGGUN
    
    state G_DrawWeaponTile
	
	    ifvarg weaponcount 0
        ifvarl weaponcount 8
        {
            setvarvar hud_tilenum weaponcount
            shiftvarr hud_tilenum 1
            addvar hud_tilenum RPGGUN
            
            state G_DrawWeaponTile
        }
ends

actor RPGSPRITE
    state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
	}
    else ifp pshrunk
	{
	}
    else ifp palive ifpdistl RETRIEVEDISTANCE ifcount 6 ifcanseetarget
    {
        getplayervar[THISACTOR].HAS_RPG TEMP2
		ifvare TEMP2 1
			state checkweaponstay
		
		getplayervar[THISACTOR].RPG_AMMO TEMP
		ifvarl TEMP MAXRPGAMMO // Ammo under max?
		{
			addvarvar TEMP RPGAMMOBOX
			ifvarg TEMP MAXRPGAMMO // If over, cap it.
			{
				setvar TEMP MAXRPGAMMO
			}
			
			setplayervar[THISACTOR].RPG_AMMO TEMP
		}
		else
		{
			ifvare TEMP2 1 // Has weapon?
			{
				break // Has weapon, and full ammo. Stop here.
			}
		}
		
		setplayervar[THISACTOR].HAS_RPG 1 // Give it
		setvar STATE_PARAM1 RPG_WEAPON
		state addweapon_nobreak
        quote 56
        ifspawnedby RPGSPRITE
		{
            state getweaponcode
		}
        else
		{
            state quikweaponget_custom
		}
    }
enda

actor RPGAMMO
	state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
    }
    else ifp pshrunk
	{
    }
    else ifp palive ifcount 6 ifpdistl RETRIEVEDISTANCE ifcanseetarget 
    {
		getplayervar[THISACTOR].RPG_AMMO TEMP
		ifvarl TEMP MAXRPGAMMO
		{
			addvarvar TEMP RPGAMMOBOX
			ifvarg TEMP MAXRPGAMMO
			{
				setvar TEMP MAXRPGAMMO
			}
			
			setplayervar[THISACTOR].RPG_AMMO TEMP
			
			quote 64
			ifspawnedby RPGAMMO {
				state getcode
			}
			else {
				state quikget
			}
		}
    }
enda