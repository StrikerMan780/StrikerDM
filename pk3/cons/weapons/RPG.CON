state draw_rpg // RPG_WEAPON:
	guniqhudid W_RPG
	setvar hud_tilenum RPGGUN
	
	setvarvar hud_x weapon_xoffset
	setvarvar hud_y looking_arc
	shiftvarl hud_y 1
	subvarvar hud_y gun_pos
	
	setvarvar hud_temp weaponcount
	shiftvarl hud_temp 7
	addvar hud_temp 768
	andvar hud_temp 2047
	sin hud_temp hud_temp
	shiftvarr hud_temp 11
	
	subvarvar hud_x hud_temp
	subvarvar hud_y hud_temp
	
	addvar hud_x 164
	addvar hud_y 176
	
	ifvarg weaponcount 0
		orvar hud_orientation RS_FORCELERP
	
	// Rotations
	switch weaponcount
		case 1
			addvar hud_x 4
			addvar hud_scale 1024
			setvar hud_angle 8
			break
		case 2
			addvar hud_x 8
			addvar hud_scale 2048
			setvar hud_angle 16
			break
		case 3
			addvar hud_x 16
			addvar hud_scale 4096
			setvar hud_angle 32
			break
		case 4
			addvar hud_x 32
			addvar hud_scale 8192
			setvar hud_angle 64
			break
		case 5
			addvar hud_x 24
			addvar hud_scale 6144
			setvar hud_angle 48
			break
		case 6
			addvar hud_x 16
			addvar hud_scale 4096
			setvar hud_angle 32
			break
		case 7
			addvar hud_x 8
			addvar hud_scale 2048
			setvar hud_angle 16
			break
		case 8
			addvar hud_x 4
			addvar hud_scale 1024
			setvar hud_angle 8
			break
		case 10
			addvar hud_x -4
			setvar hud_angle -8
			break
		case 11
			addvar hud_x -8
			setvar hud_angle -16
			break
		case 12
			addvar hud_x -4
			setvar hud_angle -8
			break
		case 13
			addvar hud_x -2
			setvar hud_angle -4
			break
	endswitch
	
	state G_DrawWeaponTile

	ifvarg weaponcount 0
	ifvarl weaponcount 8
	{
		guniqhudid W_RPG_MUZZLE
		switch weaponcount
			case 1: // Interp hack
				setvar hud_tilenum 2545
				setvar hud_alpha 255
				break
			case 2:
			case 3:
				setvar hud_tilenum 2545
				break
			case 4:
			case 5:
				setvar hud_tilenum 2546
				break
			case 6:
			case 7:
				setvar hud_tilenum 2547
				break
		endswitch
		
		orvar hud_orientation RS_FORCELERP
		state G_DrawWeaponTile
	}
	
	guniqhudid 0
ends

actor RPGSPRITE
    state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
	}
    else ifp pshrunk
	{
	}
    else ifp palive ifpdistl RETRIEVEDISTANCE ifcount 6 ifcanseetarget
    {
		// Parameters
		setvar STATE_PARAM1 RPG_WEAPON
		setvar STATE_PARAM2 WEAP_PRIMARY
		setvar STATE_PARAM3 1
		
		state check_have_weapon
		ifvare STATE_RETURN 1
			state checkweaponstay
		
		getplayervar[THISACTOR].RPG_AMMO TEMP
		ifvarl TEMP MAXRPGAMMO // Ammo under max?
		{
			addvarvar TEMP RPGAMMOBOX
			ifvarg TEMP MAXRPGAMMO // If over, cap it.
			{
				setvar TEMP MAXRPGAMMO
			}
			
			setplayervar[THISACTOR].RPG_AMMO TEMP
		}
		else
		{
			ifvare STATE_RETURN 1 // Has weapon?
			{
				break // Has weapon, and full ammo. Stop here.
			}
		}
		
		state set_have_weapon
		state addweapon_nobreak
        quote 56
        ifspawnedby RPGSPRITE
		{
            state getweaponcode
		}
        else
		{
            state quikweaponget_custom
		}
    }
enda

actor RPGAMMO
	state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
    }
    else ifp pshrunk
	{
    }
    else ifp palive ifcount 6 ifpdistl RETRIEVEDISTANCE ifcanseetarget 
    {
		getplayervar[THISACTOR].RPG_AMMO TEMP
		ifvarl TEMP MAXRPGAMMO
		{
			addvarvar TEMP RPGAMMOBOX
			ifvarg TEMP MAXRPGAMMO
			{
				setvar TEMP MAXRPGAMMO
			}
			
			setplayervar[THISACTOR].RPG_AMMO TEMP
			
			quote 64
			ifspawnedby RPGAMMO {
				state getcode
			}
			else {
				state quikget
			}
		}
    }
enda