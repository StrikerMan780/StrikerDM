state draw_shrinker // SHRINKER_WEAPON:
    state reset_hud_weapon_coordinates
    
    addvar hud_x 28
    addvar hud_y 18
    
    setvarvar hud_temp4 hud_pal
    setvar hud_pal 0
    
    setvar hud_temp2 0
    
    ifvarg weaponcount 0
    {
        ifvarvarl weaponcount hud_totaltime
        {
            state determine_animation
            ifvarn hud_temp 1
            {
                displayrand hud_temp
                andvar hud_temp 3
				
				addvarvar hud_x hud_temp

                displayrand hud_temp
                andvar hud_temp 3

                subvarvar hud_y hud_temp
            }
            
            addvar hud_x 184
            addvar hud_y 240
            
			// Crystal
			guniqhudid W_SHRINKER_CRYSTAL
            setvarvar hud_temp3 weaponcount
            modvar hud_temp3 6
			
			switch hud_temp3
				case 0: setvar hud_tilenum SHRINKERCRYSTAL_FIRE1 break
				case 1: setvar hud_tilenum SHRINKERCRYSTAL_EXTRA1 break
				case 2: setvar hud_tilenum SHRINKERCRYSTAL_FIRE2 break
				case 3: setvar hud_tilenum SHRINKERCRYSTAL_EXTRA2 break
				case 4: setvar hud_tilenum SHRINKERCRYSTAL_FIRE3 break
				case 5: setvar hud_tilenum SHRINKERCRYSTAL_EXTRA3 break
				default: setvar hud_tilenum SHRINKERCRYSTAL break
			endswitch
            
            setvar hud_shade -32
			
			orvar hud_orientation RS_FORCELERP
            state G_DrawTileScaled
			// ----
            
            guniqhudid W_SHRINKER
			
            addvar hud_x 4
            
			switch weaponcount
				case 1:
				case 12:
					setvar hud_tilenum SHRINKER_EXTRA1
					break
				case 2:
				case 11:
					setvar hud_tilenum SHRINKER_EXTRA2
					break
				case 3:
				case 10:
					setvar hud_tilenum SHRINKER_EXTRA3
					break
				default:
					setvar hud_tilenum SHRINKER
					addvar hud_tilenum 1
					break
			endswitch
            
            setvarvar hud_shade gs
            setvarvar hud_pal hud_temp4
            
			orvar hud_orientation RS_FORCELERP
            state G_DrawWeaponTile
            
            guniqhudid 0
    
            setvar hud_temp2 1
        }
    }

    ifvare hud_temp2 0
    {
        addvar hud_x 184
        addvar hud_y 240
        
		// Crystal
		guniqhudid W_SHRINKER_CRYSTAL
        setvar hud_tilenum SHRINKERCRYSTAL
        
        getplayer[THISACTOR].random_club_frame hud_shade
        andvar hud_shade 2047
        sin hud_shade hud_shade
        shiftvarr hud_shade 10
        mulvar hud_shade -1
        addvar hud_shade 16
        
        state G_DrawTileScaled
		// ----
        
        guniqhudid W_SHRINKER
        
        addvar hud_x 4
        
        setvar hud_tilenum SHRINKER
        
        setvarvar hud_shade gs
        setvarvar hud_pal hud_temp4
        
        state G_DrawWeaponTile
        
        guniqhudid 0
    }
ends

spritenoshade SHRINKERTRAIL
actor SHRINKERTRAIL 0
{
	ifaction 0
	{
		ifpdistl 1024
		{
			killit
			break
		}
		
		action ANULLACTION
	}
		
	ifcount 12
		killit
}
enda