gamevar WEAPON11_FIREDELAY 2 1

state draw_expander // GROW_WEAPON:
    state reset_hud_weapon_coordinates
    
    addvar hud_x 28
    addvar hud_y 18
    
    setvarvar hud_temp4 hud_pal
    setvar hud_pal 19 // Changed from 2 to 19 to leave bolts white
    
    setvarvar hud_temp3 currentweapon
    shiftvarl hud_temp3 1
    
    setvar hud_temp2 0
    
    ifvarg weaponcount 0
    {
        ifvarvarl weaponcount hud_totaltime
        {
            state determine_animation
            ifvarn hud_temp 1
            {
                displayrand hud_temp
                andvar hud_temp 3
				
				addvarvar hud_x hud_temp

                displayrand hud_temp
                andvar hud_temp 3

                subvarvar hud_y hud_temp
            }
            
            addvar hud_x 184
            addvar hud_y 240
            
            setvarvar hud_temp3 weaponcount
            modvar hud_temp3 6
			
			switch hud_temp3
				case 0: setvar hud_tilenum SHRINKERCRYSTAL_FIRE1 break
				case 1: setvar hud_tilenum SHRINKERCRYSTAL_EXTRA1 break
				case 2: setvar hud_tilenum SHRINKERCRYSTAL_FIRE2 break
				case 3: setvar hud_tilenum SHRINKERCRYSTAL_EXTRA2 break
				case 4: setvar hud_tilenum SHRINKERCRYSTAL_FIRE3 break
				case 5: setvar hud_tilenum SHRINKERCRYSTAL_EXTRA3 break
				default: setvar hud_tilenum SHRINKERCRYSTAL break
			endswitch
            
            setvar hud_shade -32
        
            guniqhudid hud_temp3
            
            state G_DrawWeaponTile_crystal
            
            guniqhudid currentweapon
            
            addvar hud_x 4
            
			switch weaponcount
				case 2:
					setvar hud_tilenum EXPANDER_EXTRA3
					break
				case 3:
					setvar hud_tilenum EXPANDER_EXTRA2
					break
				case 4:
					setvar hud_tilenum EXPANDER_EXTRA1
					break
				default:
					setvar hud_tilenum EXPANDER2
					break
			endswitch
            
            setvarvar hud_shade gs
            
            setvarvar hud_pal hud_temp4
            
            state G_DrawWeaponTile
            
            guniqhudid 0
    
            setvar hud_temp2 1
        }
    }
    ifvare hud_temp2 0
    {
        addvar hud_x 184
        
        addvar hud_y 240
        
        setvar hud_tilenum SHRINKERCRYSTAL
        
        getplayer[THISACTOR].random_club_frame hud_shade
        andvar hud_shade 2047
        sin hud_shade hud_shade
        shiftvarr hud_shade 10
        mulvar hud_shade -1
        addvar hud_shade 16
        
        guniqhudid hud_temp3
        
        state G_DrawWeaponTile_crystal
        
        guniqhudid currentweapon
        
        addvar hud_x 4
        
        setvar hud_tilenum EXPANDER
        
        setvarvar hud_shade gs
        
        setvarvar hud_pal hud_temp4
        
        state G_DrawWeaponTile
        
        guniqhudid 0
    }
ends

spritenoshade GROWSPARK
defineprojectile GROWSPARK PROJ_WORKSLIKE 98314
defineprojectile GROWSPARK PROJ_CSTAT 130
defineprojectile GROWSPARK PROJ_VEL 768
defineprojectile GROWSPARK PROJ_VEL_MULT 3
defineprojectile GROWSPARK PROJ_XREPEAT 24
defineprojectile GROWSPARK PROJ_YREPEAT 24
defineprojectile GROWSPARK PROJ_EXTRA GROWSPARK_WEAPON_STRENGTH
defineprojectile GROWSPARK PROJ_EXTRA_RAND 5
defineprojectile GROWSPARK PROJ_CLIPDIST 16
defineprojectile GROWSPARK PROJ_HITRADIUS 0
defineprojectile GROWSPARK PROJ_SHADE -127
defineprojectile GROWSPARK PROJ_ISOUND SHRINKER_HIT
defineprojectile GROWSPARK PROJ_SPAWNS -1
defineprojectile GROWSPARK PROJ_SXREPEAT 32
defineprojectile GROWSPARK PROJ_SYREPEAT 32
defineprojectile GROWSPARK PROJ_TRAIL EXPANDERTRAIL
defineprojectile GROWSPARK PROJ_TXREPEAT 24
defineprojectile GROWSPARK PROJ_TYREPEAT 24

state expander_radius
	headspritestat CURSPRITE 10 // STAT_PLAYER
	setvarvar LASTSPRITE CURSPRITE
	whilevarn CURSPRITE -1
	{
		getactor[THISACTOR].owner TEMP
		getactor[TEMP].yvel TEMP
		getactor[CURSPRITE].yvel TEMP2
		
		ifvarvarn TEMP TEMP2
		{
			dist TEMP THISACTOR CURSPRITE
			ifvarl TEMP 1280
			{
				getactor[CURSPRITE].extra TEMP
				ifvarg TEMP 0
				{
					getactor[CURSPRITE].cstat TEMP
					ifvarand TEMP 256 // Shootable
					{	
						getactor[THISACTOR].owner TEMP
						setactor[CURSPRITE].htowner TEMP
						setactor[CURSPRITE].htpicnum GROWSPARK
						
						getactor[CURSPRITE].extra TEMP
						subvar TEMP GROWSPARK_WEAPON_STRENGTH
						ifvarl TEMP 1
						{
							getactor[CURSPRITE].htextra TEMP
							addvar TEMP 999
							setactor[CURSPRITE].htextra TEMP
						}
						else
						{
							setactor[CURSPRITE].extra TEMP
							getactor[CURSPRITE].htextra TEMP
							addvar TEMP 1
							setactor[CURSPRITE].htextra TEMP
						}
					}
				}
			}
		}
			
		nextspritestat CURSPRITE LASTSPRITE
		setvarvar LASTSPRITE CURSPRITE
	}
ends


spriteflags EXPANDEREXPLOSION 4
action EXPANDERFRAMES 0 4 1 1 10
actor EXPANDEREXPLOSION 0
	ifaction 0
	{
		cstat 130
		sizeat 32 32
		
		setactor[THISACTOR].shade -127
		setactor[THISACTOR].blend 255
		
		espawn FIRESHELL
		setactor[RETURN].pal 199
		setvar RETURN 0
		
		action EXPANDERFRAMES
		spawn EXPANDERTRAIL
		state expander_radius
	}
	else
	{
		sizeto 32 0
		sizeto 32 0
		ifactioncount 4
			killit
	}
enda

spritenoshade EXPANDERTRAIL
actor EXPANDERTRAIL 0
{
	ifaction 0
	{
		ifpdistl 1024
		{
			killit
			break
		}
		
		action ANULLACTION
	}
	
	ifcount 6
		killit
}
enda
