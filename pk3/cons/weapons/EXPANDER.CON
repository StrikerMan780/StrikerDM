state draw_expander // GROW_WEAPON:
    state reset_hud_weapon_coordinates
    
    addvar hud_x 28
    addvar hud_y 18
    
    setvarvar hud_temp4 hud_pal
    setvar hud_pal 2
    
    setvarvar hud_temp3 currentweapon
    shiftvarl hud_temp3 1
    
    setvar hud_temp2 0
    
    ifvarg weaponcount 0
    {
        ifvarvarl weaponcount hud_totaltime
        {
            state determine_animation
            ifvarn hud_temp 1
            {
                displayrand hud_temp
                andvar hud_temp 3
				
				addvarvar hud_x hud_temp

                displayrand hud_temp
                andvar hud_temp 3

                subvarvar hud_y hud_temp
            }
            
            addvar hud_x 184
            addvar hud_y 240
            
            setvarvar hud_tilenum weaponcount
            andvar hud_tilenum 3
            addvar hud_tilenum SHRINKER
            addvar hud_tilenum 3
            
            setvar hud_shade -32
        
            guniqhudid hud_temp3
            
            state G_DrawWeaponTile_crystal
            
            guniqhudid currentweapon
            
            addvar hud_x 4
            
            setvar hud_tilenum SHRINKER
            subvar hud_tilenum 1
            
            setvarvar hud_shade gs
            
            setvarvar hud_pal hud_temp4
            
            state G_DrawWeaponTile
            
            guniqhudid 0
    
            setvar hud_temp2 1
        }
    }
    ifvare hud_temp2 0
    {
        addvar hud_x 184
        
        addvar hud_y 240
        
        setvar hud_tilenum SHRINKER
        addvar hud_tilenum 2
        
        getplayer[THISACTOR].random_club_frame hud_shade
        andvar hud_shade 2047
        sin hud_shade hud_shade
        shiftvarr hud_shade 10
        mulvar hud_shade -1
        addvar hud_shade 16
        
        guniqhudid hud_temp3
        
        state G_DrawWeaponTile_crystal
        
        guniqhudid currentweapon
        
        addvar hud_x 4
        
        setvar hud_tilenum SHRINKER
        subvar hud_tilenum 2
        
        setvarvar hud_shade gs
        
        setvarvar hud_pal hud_temp4
        
        state G_DrawWeaponTile
        
        guniqhudid 0
    }
ends

spritenoshade GROWSPARK
defineprojectile GROWSPARK PROJ_WORKSLIKE 98314
defineprojectile GROWSPARK PROJ_CSTAT 128
defineprojectile GROWSPARK PROJ_VEL 768
defineprojectile GROWSPARK PROJ_VEL_MULT 3
defineprojectile GROWSPARK PROJ_XREPEAT 24
defineprojectile GROWSPARK PROJ_YREPEAT 24
defineprojectile GROWSPARK PROJ_EXTRA GROWSPARK_WEAPON_STRENGTH
defineprojectile GROWSPARK PROJ_EXTRA_RAND 5
defineprojectile GROWSPARK PROJ_CLIPDIST 8
defineprojectile GROWSPARK PROJ_HITRADIUS 900
defineprojectile GROWSPARK PROJ_SHADE -127
defineprojectile GROWSPARK PROJ_ISOUND SHRINKER_HIT
defineprojectile GROWSPARK PROJ_SPAWNS EXPANDEREXPLOSION
defineprojectile GROWSPARK PROJ_SXREPEAT 32
defineprojectile GROWSPARK PROJ_SYREPEAT 32
defineprojectile GROWSPARK PROJ_TRAIL EXPANDERTRAIL
defineprojectile GROWSPARK PROJ_TXREPEAT 24
defineprojectile GROWSPARK PROJ_TYREPEAT 24

spritenoshade EXPANDEREXPLOSION
action EXPANDERFRAMES 0 4 1 1 10
actor EXPANDEREXPLOSION 0
	ifaction 0
	{
		cstat 128
		setactor[THISACTOR].shade -127
		action EXPANDERFRAMES
	}
	else
	{
		sizeto 32 0
		sizeto 32 0
		ifactioncount 4
			killit
	}
enda

spritenoshade EXPANDERTRAIL
actor EXPANDERTRAIL 0
{
	ifaction 0
	{
		cstat 128
		sizeat 4 4
		setactor[THISACTOR].shade -127
		action ANULLACTION
	}
	
	getactor[THISACTOR].xrepeat TEMP
	addvar TEMP 4
	setactor[THISACTOR].xrepeat TEMP
	setactor[THISACTOR].yrepeat TEMP
	
	ifvarg TEMP 16
	{
		killit
	}
	else ifvarg TEMP 12
	{
		cstator 512
	}
	else ifvarg TEMP 8
	{
		cstator 2
	}
}
enda
