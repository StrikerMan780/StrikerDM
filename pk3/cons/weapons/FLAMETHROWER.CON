state draw_flamethrower // FLAMETHROWER_WEAPON:
    state reset_hud_weapon_coordinates
    
    setvar hud_tilenum FLAMETHROWER
    
    setvar hud_temp3 0
	
    ifvarg weaponcount 0
    {	
        addvar hud_totaltime 1
        ifvarvarl weaponcount hud_totaltime
        {
            state determine_animation
			
            ifvarn hud_temp 1
            {
                displayrand hud_temp
                andvar hud_temp 3
                
                addvarvar hud_x hud_temp
				
				displayrand hud_temp
                andvar hud_temp 3
				
                addvarvar hud_y hud_temp
            }
            
            guniqhudid 0
            
            addvar hud_x 210
            
            addvar hud_y 277 // 16 + 261
            
            addvar hud_tilenum 1
            
            setvar hud_shade -32
            
            state G_DrawWeaponTile
            
            guniqhudid currentweapon
            
            subvar hud_y 26 // relative
            
            setvarvar hud_temp weaponcount
            modvar hud_temp 6
            divvar hud_temp 2
            addvar hud_tilenum 1
            addvarvar hud_tilenum hud_temp
            
            state G_DrawWeaponTile
            
            guniqhudid 0
            
            setvar hud_temp3 1
        }
    }
    ifvare hud_temp3 0
    {
        guniqhudid currentweapon
        
        addvar hud_x 210
        
        addvar hud_y 261
        
        state G_DrawWeaponTile
        
        guniqhudid 0
    }
ends

actor FLAMETHROWERSPRITE
    fall
	state init_pickup
	state despawn_item_from_can
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
	}
    else ifp pshrunk
	{
	}
    else ifp palive ifpdistl RETRIEVEDISTANCE ifcount 6 ifcanseetarget
    {
        getplayervar[THISACTOR].HAS_FLAMETHROWER AMMO_TEMP2
		ifspawnedby FLAMETHROWERSPRITE
		{
			ifmultiplayer
			ifvarn COOP GM_DUKEMATCH
			ifvarn COOP GM_DUKEMATCH_NOSPAWN
			ifvarn COOP GM_TEAMDUKEMATCH
			ifvarn COOP GM_TEAMDUKEMATCH_NOSPAWN
			ifvare AMMO_TEMP2 1
			{
				break
			}
		}
		
		getplayervar[THISACTOR].FLAMETHROWER_AMMO AMMO_TEMP1
		ifvarl AMMO_TEMP1 MAXFLAMETHROWERAMMO // Ammo under max?
		{
			addvarvar AMMO_TEMP1 FLAMETHROWERAMMOAMOUNT
			ifvarg AMMO_TEMP1 MAXFLAMETHROWERAMMO // If over, cap it.
			{
				setvar AMMO_TEMP1 MAXFLAMETHROWERAMMO
			}
			
			setplayervar[THISACTOR].FLAMETHROWER_AMMO AMMO_TEMP1
		}
		else
		{
			ifvare AMMO_TEMP2 1 // Has flamethrower?
			{
				break // Has weapon, and full ammo. Stop here.
			}
		}
		
		setplayervar[THISACTOR].HAS_FLAMETHROWER 1 // Give it
		
        quote 127
        ifspawnedby FLAMETHROWERSPRITE
		{
            state getweaponcode
			addweapon FREEZE_WEAPON 0
		}
        else
		{
            state quikweaponget_custom
			addweapon FREEZE_WEAPON 0
			killit
		}
    }
enda

actor FLAMETHROWERAMMO
    fall
	state init_pickup
	state despawn_item_from_can
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
    }
    else ifp pshrunk
	{
    }
    else ifp palive ifcount 6 ifpdistl RETRIEVEDISTANCE ifcanseetarget 
    {
		getplayervar[THISACTOR].FLAMETHROWER_AMMO AMMO_TEMP1
		ifvarl AMMO_TEMP1 MAXFLAMETHROWERAMMO
		{
			addvarvar AMMO_TEMP1 FLAMETHROWERAMMOAMOUNT
			ifvarg AMMO_TEMP1 MAXFLAMETHROWERAMMO
			{
				setvar AMMO_TEMP1 MAXFLAMETHROWERAMMO
			}
			
			setplayervar[THISACTOR].FLAMETHROWER_AMMO AMMO_TEMP1
			
			quote 126
			ifspawnedby FLAMETHROWERAMMO {
				state getcode
			}
			else {
				state quikget
			}
		}
    }
enda

spritenoshade FLAMETHROWERFLAME
action FLAMETHROWER_FLAME  0   10   1   1   2
actor FLAMETHROWERFLAME FLAMETHROWER_WEAPON_STRENGTH FLAMETHROWER_FLAME
    ifactioncount 10 {
		killit
    }
    ifactioncount 0 {
        cstator 130
    }
enda

spritenoshade ONFIRE
action ONFIRE_EFFECT    0    10    1    1    2
actor ONFIRE 1 ONFIRE_EFFECT
    ifactioncount 10 {
        killit
    }
    ifactioncount 2 {
        break
    }
    ifactioncount 1 {
		ifspawnedby FIREBALL
		{
			sizeat 40 40
			//iffloordistl 48
			//{
				ifrnd 128
					espawn BURNING_ALT
				else
					espawn BURNING2_ALT
				getactor[THISACTOR].owner OWNER
				setactor[RETURN].owner OWNER
				setvar RETURN 0
			//}
		}
		else
		{
			sizeat 24 24
		}
		setactor[THISACTOR].yoffset -16
		setactor[THISACTOR].shade -127
        spawn SMALLSMOKE
    }
enda

action ONFIRESMOKEFRAMES    0    9    1    1    8
actor ONFIRESMOKE 0 ONFIRESMOKEFRAMES
    ifactioncount 9
        killit
    ifactioncount 1 {
        sizeat 30 30
    }
enda

action BURNEDCORPSETHINK 0 1 1 1 1
actor BURNEDCORPSE 0
    fall
    ifaction 0 {
        sizeat 20 20
        cstator 128
        action BURNEDCORPSETHINK
    }
    ifcount 350 {
    }
    else {
        ifrnd 6 {
            spawn WHISPYSMOKE
        }
    }
enda

action WHISPYSMOKEFRAMES 0 10 1 1 8
actor WHISPYSMOKE 0 WHISPYSMOKEFRAMES
	ifcount 2 {
	}
    else ifcount 1 {
        cstator 2
        ifrnd 30 {
            sizeat 20 10 
        }
        else ifrnd 30 {
            sizeat 30 20 
        }
    }
    ifactioncount 10 {
        killit
	}
enda

spritenoshade FIREBALLTRAIL
action FIREBALL_TRAIL  0   3   1   1   2
actor FIREBALLTRAIL 0 FIREBALL_TRAIL
{

	ifactioncount 6
	{
		killit
	}
	else ifactioncount 5
	{
		sizeat 8 8
	}
	else ifactioncount 4
	{
		sizeat 12 12
	}
	else ifactioncount 3
	{
		sizeat 16 16
	}
	else ifactioncount 2
	{
		sizeat 24 24
	}
	else
	{
		ifpdistl 256
			killit
			
		cstator 130
		setactor[THISACTOR].shade -127
		
		ifinwater
		ifrnd 32
		{
			spawn WATERBUBBLE
		}
	}
} 
enda

spritenoshade FLAMETHROWERFLAME
defineprojectile FIREBALL PROJ_WORKSLIKE 36866
defineprojectile FIREBALL PROJ_XREPEAT 32
defineprojectile FIREBALL PROJ_YREPEAT 32
defineprojectile FIREBALL PROJ_VEL 644
defineprojectile FIREBALL PROJ_DROP -96
//defineprojectile FIREBALL PROJ_SPAWNS ONFIRE
defineprojectile FIREBALL PROJ_TRAIL FIREBALLTRAIL
defineprojectile FIREBALL PROJ_TXREPEAT 32
defineprojectile FIREBALL PROJ_TYREPEAT 32
defineprojectile FIREBALL PROJ_SOUND FLAMETHROWER_FIRE
defineprojectile FIREBALL PROJ_HITRADIUS 0
defineprojectile FIREBALL PROJ_CSTAT 10370
defineprojectile FIREBALL PROJ_SHADE -127
defineprojectile FIREBALL PROJ_EXTRA FLAMETHROWER_WEAPON_STRENGTH   

action BURNING_FLAME2 -2 12 1 1 2
state burningstate_flamethrower
	fall
	
	ifinwater
		killit
	
	ifcount 168
	{
		sizeto 8 8
		sizeto 8 8
		ifcount 232
			killit
	}
	else ifcount 1
	{
		sizeto 52 52
	
		ifrnd 96
		{
			hitradius 1024 1 2 3 4
		}
	}
	else
	{
		cstat 130
		setactor[THISACTOR].shade -127
	}
ends

spritenoshade BURNING_ALT
actor BURNING_ALT 0 BURNING_FLAME2
  state burningstate_flamethrower
enda

spritenoshade BURNING2_ALT
actor BURNING2_ALT 0 BURNING_FLAME2
  state burningstate_flamethrower
enda