state draw_flamethrower // FLAMETHROWER_WEAPON:
    state reset_hud_weapon_coordinates
    
    setvar hud_temp3 0
	
    ifvarg weaponcount 0
    {	
		setvar hud_tilenum FLAMETHROWER
        addvar hud_totaltime 1
        ifvarvarl weaponcount hud_totaltime
        {
            state determine_animation
			
            ifvarn hud_temp 1
            {
                displayrand hud_temp
                andvar hud_temp 3
                
                addvarvar hud_x hud_temp
				
				displayrand hud_temp
                andvar hud_temp 3
				
                addvarvar hud_y hud_temp
            }
            
			// Base
            guniqhudid W_FLAME_BASE
            
            addvar hud_x 210
            addvar hud_y 277 // 16 + 261
            
            addvar hud_tilenum 1
            setvar hud_shade -32
            
			orvar hud_orientation DRAWEAP_NOLERP
            state G_DrawWeaponTile
			// ----
            
			// Muzzle
            guniqhudid W_FLAME_TOP
            
            subvar hud_y 26 // relative
            
            setvarvar hud_temp weaponcount
            modvar hud_temp 6
            divvar hud_temp 2
            addvar hud_tilenum 1
            addvarvar hud_tilenum hud_temp
            
			orvar hud_orientation DRAWEAP_NOLERP
            state G_DrawWeaponTile
			// ----
            
            guniqhudid 0
            
            setvar hud_temp3 1
        }
    }
	
	ifvare hud_temp3 0
	{
		addvar hud_x 210
		addvar hud_y 261
		
		ifinwater
		{
		}
		else
		{
			guniqhudid W_FLAME_PILOT
			setvar hud_tilenum 5174
			
			subvar hud_shadef 32
			state G_DrawWeaponTile
			addvar hud_shadef 32
		}
		
		guniqhudid W_FLAME
		setvar hud_tilenum FLAMETHROWER
		
		state G_DrawWeaponTile
		
		guniqhudid 0
	}
ends

actor FLAMETHROWERSPRITE
    state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
	}
    else ifp pshrunk
	{
	}
    else ifp palive ifpdistl RETRIEVEDISTANCE ifcount 6 ifcanseetarget
    {
		// Parameters
		setvar STATE_PARAM1 FREEZE_WEAPON
		setvar STATE_PARAM2 WEAP_SECONDARY
		setvar STATE_PARAM3 1
		
		state check_have_weapon
		ifvare STATE_RETURN 1
			state checkweaponstay
		
		getplayervar[THISACTOR].FLAMETHROWER_AMMO TEMP
		ifvarl TEMP MAXFLAMETHROWERAMMO // Ammo under max?
		{
			addvarvar TEMP FLAMETHROWERAMMOAMOUNT
			ifvarg TEMP MAXFLAMETHROWERAMMO // If over, cap it.
			{
				setvar TEMP MAXFLAMETHROWERAMMO
			}
			
			setplayervar[THISACTOR].FLAMETHROWER_AMMO TEMP
		}
		else
		{
			ifvare STATE_RETURN 1 // Has flamethrower?
			{
				break // Has weapon, and full ammo. Stop here.
			}
		}
		
		state set_have_weapon
		state addweapon_nobreak
        quote 127
        ifspawnedby FLAMETHROWERSPRITE
		{
            state getweaponcode
		}
        else
		{
            state quikweaponget_custom
		}
    }
enda

actor FLAMETHROWERAMMO
    state item_shared_code
	
    ifmove RESPAWN_ACTOR_FLAG
	{
        state respawnit
    }
    else ifp pshrunk
	{
    }
    else ifp palive ifcount 6 ifpdistl RETRIEVEDISTANCE ifcanseetarget 
    {
		getplayervar[THISACTOR].FLAMETHROWER_AMMO TEMP
		ifvarl TEMP MAXFLAMETHROWERAMMO
		{
			addvarvar TEMP FLAMETHROWERAMMOAMOUNT
			ifvarg TEMP MAXFLAMETHROWERAMMO
			{
				setvar TEMP MAXFLAMETHROWERAMMO
			}
			
			setplayervar[THISACTOR].FLAMETHROWER_AMMO TEMP
			
			quote 126
			ifspawnedby FLAMETHROWERAMMO {
				state getcode_custom
			}
			else {
				state quikget
			}
		}
    }
enda

spritenoshade ONFIRE
action ONFIRE_EFFECT    0    10    1    1    2
actor ONFIRE 0
	ifaction 0
	{
		ifspawnedby FIREBALL
		{
			sizeat 40 40
			
			ifrnd 128
				espawn BURNING_ALT
			else
				espawn BURNING2_ALT	
			getactor[THISACTOR].owner TEMP
			setactor[RETURN].owner TEMP
			setvar RETURN 0
		}
		else
		{
			sizeat 24 24
		}
		
		spawn SMALLSMOKE
		
		setactor[THISACTOR].yoffset -16
		setactor[THISACTOR].shade -127
		setactor[THISACTOR].blend 255
		cstator 2

		action ONFIRE_EFFECT
	}
	else
	{
		ifactioncount 10
			killit
	}
enda

action ONFIRESMOKEFRAMES    0    9    1    1    8
actor ONFIRESMOKE 0 ONFIRESMOKEFRAMES
    ifactioncount 9
        killit
    ifactioncount 1 {
        sizeat 30 30
    }
enda

action BURNEDCORPSETHINK 0 1 1 1 1
actor BURNEDCORPSE 0
    fall
    ifaction 0 {
        sizeat 20 20
        cstator 128
        action BURNEDCORPSETHINK
    }
    ifcount 350 {
    }
    else {
        ifrnd 6 {
            spawn WHISPYSMOKE
        }
    }
enda

action WHISPYSMOKEFRAMES 0 10 1 1 8
actor WHISPYSMOKE 0 WHISPYSMOKEFRAMES
	ifcount 2 {
	}
    else ifcount 1
	{
        cstator 2
		ifrnd 128
		{
			cstator 4
		}
		
        ifrnd 30
		{
            sizeat 20 10 
        }
        else ifrnd 30
		{
            sizeat 30 20 
        }
		else ifrnd 30
		{
            sizeat 35 25 
        }
		else
		{
			sizeat 25 16
		}
    }
    ifactioncount 10
	{
        killit
	}
enda

spritenoshade FIREBALLTRAIL
action FIREBALL_TRAIL  0   3   1   1   2
actor FIREBALLTRAIL 0
{
	ifaction 0
	{		
		cstator 130
		setactor[THISACTOR].shade -127
		setactor[THISACTOR].blend 255
		setactor[THISACTOR].mdflags 16
		setvar ANIMATE_FLAGS ANIMATE_FLAG_ROTATE_SCALE_AND_FADE
		
		setvarvar ANIMATE_STARTCLOCK ANIMATE_CLOCK // Timer
		setvar ANIMATE_LIFETIME 32 // End time
		setvar SPAWN_ORIGANG 0 // Rotational Speed
		
		getactor[THISACTOR].xrepeat TEMP
		setvarvar XREPEAT_MEMORY TEMP // Start radius
		setvar YREPEAT_MEMORY 0 // End radius
		
		ifinwater
		ifrnd 32
		{
			spawn WATERBUBBLE
		}
		
		action FIREBALL_TRAIL
	}
	
	ifcount 8
		killit
} 
enda

spriteflags FIREBALL 1048580 // SFLAG_NOSHADE(4) + SFLAG_NODAMAGEPUSH(1048576)
defineprojectile FIREBALL PROJ_WORKSLIKE 32770
defineprojectile FIREBALL PROJ_XREPEAT 24
defineprojectile FIREBALL PROJ_YREPEAT 24
defineprojectile FIREBALL PROJ_VEL 644
defineprojectile FIREBALL PROJ_DROP -96
//defineprojectile FIREBALL PROJ_SPAWNS ONFIRE
defineprojectile FIREBALL PROJ_TRAIL FIREBALLTRAIL
defineprojectile FIREBALL PROJ_TXREPEAT 24
defineprojectile FIREBALL PROJ_TYREPEAT 24
defineprojectile FIREBALL PROJ_SOUND FLAMETHROWER_FIRE
defineprojectile FIREBALL PROJ_HITRADIUS 0
defineprojectile FIREBALL PROJ_CSTAT 10370
defineprojectile FIREBALL PROJ_SHADE -127
defineprojectile FIREBALL PROJ_EXTRA FLAMETHROWER_WEAPON_STRENGTH   

action BURNING_FLAME2 0 14 1 1 2
state burningstate_flamethrower
	fall
	
	ifinwater
		killit
		
	// Check if on water surface
	getsector[THISACTOR].lotag TEMP
	ifvare TEMP 1
	{
		// TEMP = X
		// TEMP2 = Y
		// TEMP3 = Sector Number
		getactor[THISACTOR].x TEMP
		getactor[THISACTOR].y TEMP2
		getactor[THISACTOR].sectnum TEMP3
			
		// Set TEMP to florz
		getflorzofslope TEMP3 TEMP TEMP2 TEMP
		subvar TEMP 512 // Subtract height
			
		getactor[THISACTOR].z TEMP2
		
		ifvarvarg TEMP2 TEMP // Touching floor?
		{
			spawn WHISPYSMOKE
			killit
		}
	}
	
	ifcount 128
	{
		sizeto 8 8
		sizeto 8 8
		ifcount 192
			killit
	}
	else ifcount 1
	{
		sizeto 52 52
		hitradius 800 0 0 0 0
	}
	else
	{
		cstat 130
		setactor[THISACTOR].shade -127
		setactor[THISACTOR].blend 255
	}
ends

spritenoshade BURNING_ALT
actor BURNING_ALT 0 BURNING_FLAME2
  state burningstate_flamethrower
enda

spritenoshade BURNING2_ALT
actor BURNING2_ALT 0 BURNING_FLAME2
  state burningstate_flamethrower
enda